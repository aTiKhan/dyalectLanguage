func arrayLiteralTest() {
    var arr = [[1,2], [3,4,(5,6,a:[7,8])]]
    var res = arr[0][0] + arr[1][2]["a"][1]
    assert(9, res)
}

func arrayTest1() {
    var arr = []
    arr.add(1)
    arr.add(2)
    arr.addRange([1,2,3])
    arr.removeAt(1)
    arr.remove(3)
    assert([1, 1, 2], arr)
}

func arrayTest2() {
    var arr = [1,2,3,4,5]
    arr.clear()
    arr.add(1)
    arr.add(2)
    arr.insert(0, 11)
    arr.insert(0, 11)
    arr.remove(11)
    assert([11, 1, 2], arr)
}

func arrayTest3() {
    var arr = [1,2,3,4,5,6]
    var newArr = arr.slice(2,3).toArray()
    assert([3, 4, 5], newArr)
}

func arrayTest4() {
    var arr = [4,6,1,3,2,5]
    arr.sort((x,y) => x - y)
    assert([1, 2, 3, 4, 5, 6], arr)
}

func arrayTest5() {
    var arr = [1, 2, 3, 1, 4, 5, 1]
    assert(0, arr.indexOf(1))
    assert(6, arr.lastIndexOf(1))
}

func arrayTest6() {
    var arr = [1,2,3,4,5,6]
    var res = arr.indices().toArray()
    assert([0, 1, 2, 3, 4, 5], res)
}

func arrayTest7() {
    var arr = [1,nil,2,nil,3,nil,nil,4,5,6]
    arr.compact()
    assert([1, 2, 3, 4, 5, 6], arr)
}

func arrayTest8() {
    var arr = [1,2,3,4]
    assert(4, arr.len())
}

func arrayTest9() {
    var arr = [7,4,6,1,3,2,5]
    arr.sort()
    assert([1, 2, 3, 4, 5, 6, 7], arr)
}

func arrayTest10() {
    var arr = Array.empty(4, false)
    assert([false, false, false, false], arr)
    arr = Array.empty(3, 0)
    assert([0, 0, 0], arr)
    arr = Array.empty(2)
    assert([nil, nil], arr)
}

func arrayTest11() {
    var arr = Array(1,2,3)
    assert([1,2,3], arr)
}

func arrayTest12() {
    func it() {
        yield "foo"
        yield "boo"
    }
    var arr = Array.concat([1,2,3], [4,5,6], (1,2), it())
    assert([1,2,3,4,5,6,1,2,"foo","boo"], arr)
}

func arrayTest13() {
    var xs = (1..10).toArray()
    var slice = xs.slice(4).toArray()
    assert([5,6,7,8,9,10], slice)
}

func arrayTest14() {
    var xs = [0,1,2,3,4,5,6,7,8,9]
    var res = xs[1..5].toArray()
    assert([1,2,3,4], res)
}

func arrayTest15() {
    var xs = [1..10]
    assert([1,2,3,4,5,6,7,8,9,10], xs)
}

func arrayTest18() {
    var xs = []
    xs.addRange({1,2,3,4,5})
    assert([1,2,3,4,5], xs)
}

func arrayTest19() {
    var xs = [1,2,3]
    xs.insert(1, 1.5)
    assert([1,1.5,2,3], xs)
}

func arrayTest20() {
    var xs = [1,2,3]
    xs.insertRange(1, [3,4,5])
    assert([1,3,4,5,2,3], xs)
}

func arrayTest21() {
    var xs = [1,2,3]
    var ys = [4,5,6]
    var res = xs + ys
    assert([1,2,3,4,5,6], res)
}

func arrayTest22() {
    //Related to bug #145
    var arr = []
    arr.insert(0, 1)
    assert([1], arr)
}

func arrayTest23() { //Bug #226
    var xs = [(s: 3.0, e: 4.0), (s: 1.0, e: 2.0)]
    xs.sort((x,y) => x.s - y.s)
    assert(1.0, xs[0].s)
}

func arrayTest24() { //Issue #250
    var xs = [1,2,3,4,5]
    xs.reverse()
    assert([5,4,3,2,1], xs)
}

func arrayTest25() { //#273
    var xs = [1,2,3]
    xs.swap(1,2)
    assert([1,3,2], xs)
}

func arrayTest26() { //#285
    var xs = [1..10]
    xs.removeRangeAt(3, 7)
    assert([1,2,3], xs)
    xs = [1..10]
    xs.removeRangeAt(4)
    assert([1,2,3,4], xs)
}

func arrayTest27() { //#285
    var xs = [1..10]
    xs.removeRange([1..5])
    assert([6,7,8,9,10], xs)
}

func arrayTest28() { //#285
    var xs = [1..10]
    func filter() {
        for x in xs when x % 2 == 0 {
            yield x
        }
    }
    xs.removeRange(filter())
    assert([1,3,5,7,9], xs)
}

func arrayTest29() { //#286
    var xs = [1..10]
    xs.removeAll(x => x % 2 == 0)
    assert([1,3,5,7,9], xs)
}

func arrayTest30() { //#291
    var arr = [4,6,1,3,2,5]
    Array.sort(arr, (x,y) => x - y)
    assert([1, 2, 3, 4, 5, 6], arr)
}

func arrayCopyTest1() {
    var xs = [1, 2, 3, 4, 5, 6]
    var ys = Array.empty(3)
    Array.copy(xs, 1, ys, 0, 3)
    assert([2,3,4], ys)
}

func arrayCopyTest2() {
    var xs = [1, 2, 3, 4, 5, 6]
    var ys = Array.copy(from: xs, fromIndex: 1, count: 3)
    assert([2,3,4], ys)
}

func arrayCopyTest3() {
    var xs = [1, 2, 3, 4, 5, 6]
    var ys = [999, 999, 0, 0, 0]
    ys = Array.copy(xs, 1, ys, 2, 3)
    assert([999,999,2,3,4], ys)
}

func arrayCopyTest4() {
    var xs = [1, 2, 3, 4, 5, 6]
    var ys = Array.copy(xs, count: 3)
    assert([1,2,3], ys)
}

func arrayIndexing() { //#301
    var xs = [1,2,3,4,5]
    assert(5, xs[-1])
    assert(1, xs[-5])
}