#test "Array literal"
    var arr = [[1,2], [3,4,(5,6,a: [7,8])]]
    var res = arr[0][0] + arr[1][2]["a"][1]
    assert(9, res)
#endtest

#test "Constructor"
    let arr = Array(1,2,3)
    assert([1,2,3], arr)
#endtest

#test "Methods: toString"
    var res = [12.2, "string", true].toString()
    assert("[12.2, string, true]", res)
    res = [(1,2), 4, 5].toString()
    assert("[(1, 2), 4, 5]", res)
#endtest

#test "Methods: clone"
    let xs = [1,2,3]
    assert(true, xs == xs)
    assert(false, xs.clone() == xs)
#endtest

#test "Methods: getType"
    let x = [1,2,3]
    assert(true, TypeInfo(x) == Array)
    assert(true, x.getType() == Array)
    var t = TypeInfo(x)
    assert(11, t.code)
    assert("Array", t.name)
#endtest

#test "Methods: add, addRange, remove, removeAt"
    var arr = []
    arr.add(1)
    arr.add(2)
    arr.addRange([1,2,3])
    arr.removeAt(1)
    arr.remove(3)
    assert([1, 1, 2], arr)
#endtest

#test "Methods: add, clear, insert, remove"
    let arr = [1,2,3,4,5]
    arr.clear()
    arr.add(1)
    arr.add(2)
    arr.insert(0, 11)
    arr.insert(0, 11)
    arr.remove(11)
    assert([11, 1, 2], arr)

    let xs = [1,2,3]
    xs.insert(1, 1.5)
    assert([1,1.5,2,3], xs)
#endtest

#test "Methods: slice"
    var arr = [1,2,3,4,5,6]
    var newArr = arr.slice(2,4).toArray()
    assert([3, 4, 5], newArr)
    newArr = arr.slice(2, 5).toArray()
    assert([3, 4, 5, 6], newArr)
    newArr = arr.slice(2).toArray()
    assert([3, 4, 5, 6], newArr)
    
    let xs = (1..10).toArray()
    let slice = xs.slice(4).toArray()
    assert([5,6,7,8,9,10], slice)
#endtest

#test "Methods: sort"
    var arr = [4,6,1,3,2,5]
    arr.sort((x,y) => x - y)
    assert([1, 2, 3, 4, 5, 6], arr)

    arr = [7,4,6,1,3,2,5]
    arr.sort()
    assert([1, 2, 3, 4, 5, 6, 7], arr)
#endtest

#test "Methods: indexOf, lastIndexOf"
    var arr = [1, 2, 3, 1, 4, 5, 1]
    assert(0, arr.indexOf(1))
    assert(6, arr.lastIndexOf(1))
#endtest

#test "Methods: indices"
    var arr = [1,2,3,4,5,6]
    var res = arr.indices().toArray()
    assert([0, 1, 2, 3, 4, 5], res)
#endtest

#test "Methods: compact"
    var arr = [1,nil,2,nil,3,nil,nil,4,5,6]
    arr.compact()
    assert([1, 2, 3, 4, 5, 6], arr)
#endtest

#test "Methods: len"
    var arr = [1,2,3,4]
    assert(4, arr.len())
#endtest

#test "Methods: empty"
    var arr = Array.empty(4, false)
    assert([false, false, false, false], arr)
    arr = Array.empty(3, 0)
    assert([0, 0, 0], arr)
    arr = Array.empty(2)
    assert([nil, nil], arr)
#endtest

#test "Methods: concat"
    func it() {
        yield "foo"
        yield "boo"
    }
    var arr = Array.concat([1,2,3], [4,5,6], (1,2), it())
    assert([1,2,3,4,5,6,1,2,"foo","boo"], arr)
#endtest

#test "Methods: addRange"
    var xs = []
    xs.addRange(yields { 1,2,3,4,5 })
    assert([1,2,3,4,5], xs)
#endtest

#test "Methods: insertRange"
    var xs = [1,2,3]
    xs.insertRange(1, [3,4,5])
    assert([1,3,4,5,2,3], xs)
#endtest

#test "Methods: reverse"
    var xs = [1,2,3,4,5]
    xs.reverse()
    assert([5,4,3,2,1], xs)
#endtest

#test "Methods: swap"
    var xs = [1,2,3]
    xs.swap(1,2)
    assert([1,3,2], xs)
#endtest

#test "Methods: removeRangeAt"
    var xs = [1..10]
    xs.removeRangeAt(3, 7)
    assert([1,2,3], xs)
    xs = [1..10]
    xs.removeRangeAt(4)
    assert([1,2,3,4], xs)
#endtest

#test "Methods: removeRange 1"
    var xs = [1..10]
    xs.removeRange([1..5])
    assert([6,7,8,9,10], xs)
#endtest

#test "Methods: removeRange 2" 
    //#285
    var xs = [1..10]
    func filter() {
        for x in xs when x % 2 == 0 {
            yield x
        }
    }
    xs.removeRange(filter())
    assert([1,3,5,7,9], xs)
#endtest

#test "Methods: removeAll"
    var xs = [1..10]
    xs.removeAll(x => x % 2 == 0)
    assert([1,3,5,7,9], xs)
#endtest

#test "Methods: static sort"
    var arr = [4,6,1,3,2,5]
    Array.sort(arr, (x,y) => x - y)
    assert([1, 2, 3, 4, 5, 6], arr)
#endtest

#test "Range indices"
    var xs = [0,1,2,3,4,5,6,7,8,9]
    var res = xs[1..5].toArray()
    assert([1,2,3,4,5], res)
#endtest

#test "Ranges"
    var xs = [1..10]
    assert([1,2,3,4,5,6,7,8,9,10], xs)
#endtest

#test "Addition 1"
    var xs = [1,2,3]
    var ys = [4,5,6]
    var res = xs + ys
    assert([1,2,3,4,5,6], res)
    res = xs + (5,6)
    assert([1,2,3,5,6], res)
    res = xs + yields { 11,22 }
    assert([1,2,3,11,22], res)
#endtest

#test "Addition 2" 
    //#315
    var xs = [1,2,3]
    var res = xs + "abc"
    assert([1,2,3,'a','b','c'], res)
#endtest

#test "Insert into empty array"
    //Related to bug #145
    var arr = []
    arr.insert(0, 1)
    assert([1], arr)
#endtest

#test "Complex sort"
    //Bug #226
    var xs = [(s: 3.0, e: 4.0), (s: 1.0, e: 2.0)]
    xs.sort((x,y) => x::s - y::s)
    assert(1.0, xs[0]::s)
#endtest

#test "Methods: copy 1"
    var xs = [1, 2, 3, 4, 5, 6]
    var ys = Array.empty(3)
    Array.copy(xs, 1, ys, 0, 3)
    assert([2,3,4], ys)
#endtest

#test "Methods: copy 2"
    var xs = [1, 2, 3, 4, 5, 6]
    var ys = Array.copy(from: xs, fromIndex: 1, count: 3)
    assert([2,3,4], ys)
#endtest

#test "Methods: copy 3"
    var xs = [1, 2, 3, 4, 5, 6]
    var ys = [999, 999, 0, 0, 0]
    ys = Array.copy(xs, 1, ys, 2, 3)
    assert([999,999,2,3,4], ys)
#endtest

#test "Methods: copy 4"
    var xs = [1, 2, 3, 4, 5, 6]
    var ys = Array.copy(xs, count: 3)
    assert([1,2,3], ys)
#endtest

#test "Methods: contains"
    var xs = [1..10]
    assert(true, xs.contains(1))
    assert(true, xs.contains(10))
    assert(false, xs.contains(101))
#endtest

#test "Negative indices"
    var xs = [1,2,3,4,5]
    assert(5, xs[-1])
    assert(1, xs[-5])
#endtest

#test "Complex equality"
    type FooInArray(x)
    func FooInArray == (other) => other is FooInArray()

    var res = FooInArray(0) == FooInArray(12)
    assert(true, res)
    var xs = [FooInArray(0)]
    xs.remove(FooInArray(3))
    assert([], xs)

    xs = [12, "foo", FooInArray(12), FooInArray(0)]
    res = xs.indexOf(FooInArray(42))
    assert(res, 2)
    res = xs.indexOf(FooInArray(0))
    assert(res, 2)

    xs = [12, "foo", FooInArray(12), FooInArray(0)]
    res = xs.lastIndexOf(FooInArray(12))
    assert(res, 3)

    xs = [FooInArray(1), FooInArray(2), FooInArray(3), FooInArray(4)]
    xs.removeRange([FooInArray(0)])
    assert([FooInArray(2), FooInArray(3), FooInArray(4)], xs)
#endtest