func stringLiteralTest() {
    var fst = "some\tstring with\"inner quotes\" and such (and a buck \u0024)"
    var snd = "some\u0009string with\u0022inner quotes\u0022 and such (and a buck $)"
    assert(fst, snd)
}

func stringTest1() {
    var str = "Hello, world!"
    var res = str.len()
    assert(13, res)
}

func stringTest2() {
    var str = "Hello, world!"
    assert(4, str.indexOf("o"))
    assert(8, str.lastIndexOf("o"))
    assert(8, str.indexOf("o", fromIndex: 7))
    assert(3, str.lastIndexOf("l", fromIndex: 6, count: 4))
}

func stringTest3() {  
    assert(false, "1" > "2")
    assert(true, "1" < "2")
}

func stringTest4() {
    var str = "Name=John;Surname=Doe;Age=21;Gender=Male"
    var arr = str.split('=', ';')
    assert(["Name","John","Surname","Doe","Age","21","Gender","Male"], arr)
}

func stringTest5() {
    var str = "foo"
    var res = str[0] + str[2]
    assert("fo", res)
}

func stringTest6() {
    var str1 = "FOO"
    var str2 = "bar"
    var res = str1.lower() + str2.upper()
    assert("fooBAR", res)
}

func stringTest7() {
    var res = "fooBar".startsWith("foo")
    assert(true, res)
}

func stringTest8() {
    var res = "fooBar".endsWith("Bar")
    assert(true, res)
}

func stringTest9() {
    var res = "abcdef".sub(2, 4) + "qwerty".sub(4)
    assert("cdefty", res)
}

func stringTest10() {
    var res = "camelCase".capitalize()
    assert("Camelcase", res)
}

func stringTest11() {
    var res = String.concat("one", "two", "three", 44)
    assert("onetwothree44", res)
    res = String.concat(12, true, "bool")
    assert("12truebool", res)
}

func stringTest12() {
    var res = "fooBar".contains("oBa")
    assert(true, res)
}

func stringTest13() {
    var res = " ss".trim() + "--dd--".trimStart('-') + "!ee!".trimEnd('!')
    assert("ssdd--!ee", res)
}

func stringTest14() {
    assert(true, "".isEmpty())
    assert(true, " \s ".isEmpty())
    assert(true, " \t\r\n\t ".isEmpty())
    assert(false, "hh".isEmpty())
}

func stringTest15() {
    var str = "Hello!"
    assert('H', str[0])
    assert('!', str[str.len() - 1])
}

func stringTest16() {
    var str = String.join("foo", 1, 3, 44, true, separator: ";")
    assert("foo;1;3;44;true", str)
}

func stringTest17() {
    var str = "A \("string") inside."
    assert("A string inside.", str)
    str = "A \("bar"+"foo") inside."
    assert("A barfoo inside.", str)
    str = "A \("bar"+"foo") inside with len=\("barfoo".len())."
    assert("A barfoo inside with len=6.", str)
}

func stringTest18() {
    var foo = "12"
    assert("   12", foo.padLeft(5))
    assert("00012", foo.padLeft(with: '0', to: 5))
    assert("12   ", foo.padRight(5))
    assert("12000", foo.padRight(with: '0', to: 5))
}

func stringTest19() {
    var res = String("one", "two", "three", 44)
    assert("onetwothree44", res)
}

func stringTest20() {
    assert("", String.default())
}

func stringTest21() {
    assert("for gem gemetric", "for rem remetric".replace("rem", "gem"))
    assert("foobar", "foobar".replace(value: "FOO", with: "ZOO"))
    assert("ZOObar", "foobar".replace(value: "FOO", with: "ZOO", ignoreCase: true))
}

func stringTest22() {
    var str = "from to"
    assert("from", str.remove(4))
    assert("from", str.remove(4, 3))
    assert("from ", str.remove(5))
    assert("froo", str.remove(3, 3))
}

func stringTest23() { //Bug #207
    var str = "alphaBETA"
    assert("Alphabeta", str.capitalize())
}

func stringTest24() { //Bug #219
    var str = String(values: yields { 'a','b','c' })
    assert("abc", str)
}

func stringTest25() {
    var str = String.repeat('*', 5)
    assert("*****", str)
    str = String.repeat(value: '!', count: 3)
    assert("!!!", str)
}

func stringTest26() { //Issue #249
    var str = "foobar"
    assert("raboof", str.reverse())
}

func verbatimStringTest1() {
    var str = <[ first
    second
    third..]>
    var pat = " first\n    second\n    third.."
    assert(pat, str)
}

func verbatimStringTest2() {
    var str = <[ first
    <[second]>]>
    third..]>
    var pat = " first\n    <[second]>\n    third.."
    assert(pat, str)
}

func interpolationTest1() {
    var x = 42
    var y = 12
    var res = "the value of (x + y) is \(x + y)"
    assert("the value of (x + y) is 54", res)
}

func interpolationTest2() {
    func doubleMe(x) {
        x + x
    }
    var foo = 13
    var res = "doubleMe(foo) = \(doubleMe(foo))"
    assert("doubleMe(foo) = 26", res)
}

func interpolationTest3() {
    var x = 3
    var y = 2
    var res = "value = \((x * (2 + 3)) * y)"
    assert("value = 30", res)
}

func stringIndexing() { //#301
    var xs = "abcde"
    assert('e', xs[-1])
    assert('a', xs[-5])
}

func stringSlice1() { 
    var story = "Dyalect is going to be great!"
    var res = story[-6..]
    assert("great!", res)
}

func stringSplice2() {
    var str = "the quick brown fox jumped over the lazy dogs"
    assert("lazy dogs",  str[-9..])
    assert("lazy dogs",  str[36..])
    assert("the quick",  str[..8])
    assert(str,  str[..])
}

func concatTest1() {
    var str = "str"
    assert("1str", 1 + str)
    assert("2.1str", 2.1 + str)
    assert("truestr", true + str)
    assert("nilstr", nil + str)
    func sum(x,y) { x + y }
    assert("sum(x, y)str", sum + str)
}

type __Dummy()
func concatTest2() {
    func __Dummy.toString() { "!dummy!" }
    var x = __Dummy()
    var str = "str" + x
    assert("str!dummy!", str)
    str = x + "str"
    assert("!dummy!str", str)
    __Dummy.__deleteMember("toString")
}