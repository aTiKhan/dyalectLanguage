#region "String literal"
    var fst = "some\tstring with\"inner quotes\" and such (and a buck \u0024)"
    var snd = "some\u0009string with\u0022inner quotes\u0022 and such (and a buck $)"
    assert(fst, snd)
#endregion

#region "Constructor"
    var res = String("one", "two", "three", 44)
    assert("onetwothree44", res)
    var str = String(values: yields { 'a','b','c' })
    assert("abc", str)
#endregion

#region "Methods: Default"
    assert("", String.Default)
#endregion

#region "Methods: GetType"
    let x = "Foo"
    assert(true, TypeInfo(x) == String)
    assert(true, x.GetType() == String)
    var t = TypeInfo(x)
    assert("String", t.name)
#endregion

#region "Methods: Length"
    var str = "Hello, world!"
    var res = str.Length()
    assert(13, res)
#endregion

#region "Methods: IndexOf, LastIndexOf"
    var str = "Hello, world!"
    assert(4, str.IndexOf("o"))
    assert(8, str.LastIndexOf("o"))
    assert(8, str.IndexOf("o", index: 7))
    assert(3, str.LastIndexOf("l", index: 6, count: 4))
#endregion

#region "Comparison"
    assert(false, "1" > "2")
    assert(true, "1" < "2")
#endregion

#region "Methods: Split"
    var str = "Name=John;Surname=Doe;Age=21;Gender=Male"
    var arr = str.Split('=', ';')
    assert(["Name","John","Surname","Doe","Age","21","Gender","Male"], arr)
#endregion

#region "Addition"
    var str = "foo"
    var res = str[0] + str[2]
    assert("fo", res)
#endregion

#region "Methods: Lower, Upper"
    var str1 = "FOO"
    var str2 = "bar"
    var res = str1.Lower() + str2.Upper()
    assert("fooBAR", res)
#endregion

#region "Methods: StartsWith, EndsWith"
    var res = "fooBar".StartsWith("foo")
    assert(true, res)
    res = "fooBar".EndsWith("Bar")
    assert(true, res)
#endregion

#region "Methods: Substring"
    var res = "abcdef".Substring(2, 4) + "qwerty".Substring(4)
    assert("cdefty", res)
    res = "abcdef".Substring(-3)
    assert("def", res)
#endregion

#region "Methods: Capitalize"
    var res = "camelCase".Capitalize()
    assert("Camelcase", res)
    var str = "alphaBETA"
    assert("Alphabeta", str.Capitalize())
#endregion

#region "Methods: Concat"
    var res = String.Concat("one", "two", "three", 44)
    assert("onetwothree44", res)
    res = String.Concat(12, true, "bool")
    assert("12truebool", res)
#endregion

#region "Methods: Contains"
    var res = "fooBar".Contains("oBa")
    assert(true, res)
#endregion

#region "Methods: Trim, TrimStart, TrimEnd"
    var res = " ss".Trim() + "--dd--".TrimStart('-') + "!ee!".TrimEnd('!')
    assert("ssdd--!ee", res)
#endregion

#region "Methods: IsEmpty"
    assert(true, "".IsEmpty())
    assert(true, " \s ".IsEmpty())
    assert(true, " \t\r\n\t ".IsEmpty())
    assert(false, "hh".IsEmpty())
#endregion

#region "Indexing"
    var str = "Hello!"
    assert('H', str[0])
    assert('!', str[str.Length() - 1])
    var xs = "abcde"
    assert('e', xs[-1])
    assert('a', xs[-5])
#endregion

#region "Slicing"
    var story = "Dyalect is going to be great!"
    var res = story[-6..]
    assert("great!", res)

    var str = "the quick brown fox jumped over the lazy dogs"
    assert("lazy dogs", str[-9..])
    assert("lazy dogs", str[36..])
    assert("the quick", str[..8])
    assert(str, str[..])
#endregion

#region "Methods: Join"
    var str = String.Join("foo", 1, 3, 44, true, separator: ";")
    assert("foo;1;3;44;true", str)
#endregion

#region "Methods: PadLeft, PadRight"
    var foo = "12"
    assert("   12", foo.PadLeft(5))
    assert("00012", foo.PadLeft(width: 5, char: '0'))
    assert("12   ", foo.PadRight(5))
    assert("12000", foo.PadRight(width: 5, char: '0'))
#endregion

#region "Methods: Replace"
    assert("for gem gemetric", "for rem remetric".Replace("rem", "gem"))
    assert("foobar", "foobar".Replace(value: "FOO", other: "ZOO"))
    assert("ZOObar", "foobar".Replace(value: "FOO", other: "ZOO", ignoreCase: true))
#endregion

#region "Methods: Remove"
    var str = "from to"
    assert("from", str.Remove(4))
    assert("from", str.Remove(4, 3))
    assert("from ", str.Remove(5))
    assert("froo", str.Remove(3, 3))
#endregion

#region "Methods: Repeat"
    var str = String.Repeat('*', 5)
    assert("*****", str)
    str = String.Repeat(value: '!', count: 3)
    assert("!!!", str)
#endregion

#region "Methods: Reverse"
    var str = "foobar"
    assert("raboof", str.Reverse())
#endregion

#region "Multiline string 1"
    var str = <[ first
    second
    third..]>
    var pat = " first\n    second\n    third.."
    assert(pat, str)
#endregion

#region "Multiline string 2"
    var str = <[ first
    <[second]>]>
    third..]>
    var pat = " first\n    <[second]>\n    third.."
    assert(pat, str)
#endregion

#region "Interpolation 1"
    var x = 42
    var y = 12
    var res = "the value of (x + y) is \(x + y)"
    assert("the value of (x + y) is 54", res)
    assert("[1, 2, 3]", "\([1,2,3])")
    assert("(1, 2, 3)", "\((1,2,3))")
#endregion

#region "Interpolation 2"
    func doubleMe(x) {
        x + x
    }
    var foo = 13
    var res = "doubleMe(foo) = \(doubleMe(foo))"
    assert("doubleMe(foo) = 26", res)
#endregion

#region "Interpolation 3"
    var x = 3
    var y = 2
    var res = "value = \((x * (2 + 3)) * y)"
    assert("value = 30", res)
#endregion

#region "Concatenation 1"
    var str = "str"
    assert("1str", 1 + str)
    assert("2.1str", 2.1 + str)
    assert("truestr", true + str)
    assert("nilstr", nil + str)
    func sum(x,y) { x + y }
    assert("sum(x, y)str", sum + str)
#endregion

#region "Concatenation 2"
    type TypeInString()
    func TypeInString.ToString() => "!dummy!"

    var x = TypeInString()
    var str = "str" + x
    assert("str!dummy!", str)
    str = x + "str"
    assert("!dummy!str", str)
#endregion

#region "Methods: ToCharArray"
    let str = "Hello"
    let arr = str.ToCharArray()
    assert(['H', 'e', 'l', 'l', 'o'], arr)
#endregion

#region "Methods: Repeat"
    let str = String.Repeat("ha", 5)
    assert("hahahahaha", str)
#endregion

#region "Methods: ToLiteral"
    let str = "Foo"
    assert("\"Foo\"", str.ToLiteral())
    assert("'F'", str[0].ToLiteral())
#endregion

#region "Conversion: as 1"
    let str = "12"
    assert('1', str as Char)
    assert(12, str as Integer)
    assert(12.0, str as Float)
    assert(true, str as Bool)
    assert(('1','2'), str as Tuple)
    assert(['1','2'], str as Array)
#endregion

#region "Conversion: as 2"
    let xs = "Hello,world!"
    let ys = String(values: xs as Set)
    assert("Helo,wrd!", ys)
#endregion

#region "Format 1"
    let author = "Mahesh Chand"
    let book = "Graphics Programming with GDI+"
    let year = 2003
    let price = 49.95
    let publisher = "APress"
    let fmt = "{0} is the author of book {1}, published by {2} in year {3}. Book price is ${4}."
    let res1 = String.Format(fmt, author, book, publisher, year, price)
    let res2 = fmt.Format(author, book, publisher, year, price)
    assert("Mahesh Chand is the author of book Graphics Programming with GDI+, published by APress in year 2003. Book price is $49.95.", res1)
    assert(res1, res2)
#endregion

#region "Format 2"
    let data = String.Format("{0,-35} {1,-20} {2,-10} {3, -10}", "Foo", "Boo", "Bar", "Zar")
    assert("Foo                                 Boo                  Bar        Zar       ", data)
#endregion

#region "Operator in"
    assert(true, 'o' in "foo")
    assert(false, 'B' in "bar")
    assert(true, "ello" in "Hello, world")
#endregion

#region "Methods: ToSet"
    let xs = "Hello,world!"
    let ys = String(values: xs.ToSet())
    assert("Helo,wrd!", ys)
#endregion