
func iteratorLiteralTest1() {
    var seq = yields { 1,2,3 }
    assert([1,2,3], seq.toArray())
}

func iteratorLiteralTest2() {
    var x = 2
    var y = 3
    var seq = yields { x + y }
    x -= 1
    y *= y
    assert(10, seq.first())
}

func iteratorLiteralTest3() {
    var xs = yields { 1, 2, 3, 4, 5 }
    assert(5, xs.len())
}

func iteratorLiteralTest4() {
    var xs = yields { 1, 2, 3, 4, 5 }
    assert(5, xs.len())
    assert(5, xs.len())
    xs.first()
    assert(5, xs.len())
    xs.last()
    assert(5, xs.len())
}

func iteratorLiteralTest5() {
    var str = String.concat(values: yields {
        22 + 1.25,
        "Hello, world!",
        true
    })
    assert("23.25Hello, world!true", str)
}

func iteratorTest1() {
    func foo(token) {
        yield 1
        yield 3
        yield 44.2
        if token {
            yield break
        }
        yield 68.6
    }
    var it = foo(false)
    var res = it.element(at: 0) + it.element(at: 1) +
        it.element(at: 2) + it.element(at: 3)
    assert(116.8, res)
}

func iteratorTest2() {
    var arr = [1,2,3,4,5,6,7,8]
    func sum(arr) {
        var acc = 0
        for x in arr {
            acc += x
        }
        acc
    }
    var res = sum(arr)
    assert(36, res)
}

func iteratorTest3() {
    var arr = [1,2,3,4,5,6,7,8]
    func filter(arr, pred) {
        for x in arr {
            if pred(x) {
                yield x
            }
        }
    }
    func sum(arr) {
        var acc = 0
        for x in arr {
            acc += x
        }
        acc
    }
    var filtered = filter(arr, x => x % 2 == 0)
    var res = sum(filtered)
    assert(20, res)
}

func iteratorTest4() {
    func reverse(arr) {
        var newArr = []
        newArr.addRange(arr) //hack
        var len = newArr.len()
        var counter = 1
        for x in arr {
            newArr[len - counter] = x
            counter += 1
        }
        newArr
    }
    func makeString(arr) {
        var str = ""
        for x in arr {
            str += x
        }
        str
    }
    let arr = reverse("Hello")
    var res = makeString(arr)
    assert("olleH", res)
}

func iteratorTest5() {
    func filter(arr, pred) {
        for x in arr {
            if pred(x) {
                yield x
            }
        }
    }
    func toArray(seq) {
        var arr = []
        for x in seq {
            arr.add(x)
        }
        arr
    }
    let arr = toArray( filter("hello", x => x == 'l') )
    var res = arr[0] + arr[1]
    assert("ll", res)
}

func iteratorTest6() {
    var arr = [1,3,5,8]
    var x =
        for x in arr {
            if x % 2 == 0 {
                break x
            }
        }
    assert(8, x)
}

func iteratorTest7() {
    func Integer.iter() {
        var i = 0
        if this < 0 {
            while i > this {
                yield i
                i -= 1
            }
        } else {
            while i < this {
                yield i
                i += 1
            }
        }
    }

    var res = 5.iter().toArray()
    Integer.__deleteMember("iter")
    assert([0,1,2,3,4], res)
}

func iteratorTest8() {
    var xs = 1..10
    assert(10, xs.len())
    for x in xs {
        if x > 3 {
            break
        }
    }
    xs.first()
    assert(10, xs.len())
}

func iteratorTest9() {
    var xs = 1..5
    var ys = 6..10
    var res = Iterator.concat(xs, ys)
    assert([1..10], res.toArray())
}

func iteratorTest10() {
    var xs = 1..5
    var ys = 6..10
    var res = Iterator(xs, ys)
    assert([1..10], res.toArray())
}

func iteratorTest11() { //#289
    var xs = 1..5
    var res = Iterator(xs)
    assert((1,2,3,4,5), res.toTuple())
}

func yieldManyTest1() {
    func bar() {
        yield 4
        yield 5
        yield 6
    }
    func foo() {
        yield 1
        yield 2
        yield 3
        yield many bar()
    }
    var seq = foo().toArray()
    assert([1,2,3,4,5,6],seq)
}

func yieldManyTest2() {
    func bar() {
        yield 31
        yield 32
        yield 33
    }
    func foo() {
        yield 1
        yield 2
        yield 3
        yield many bar()
        yield 4
        yield 5
    }
    var seq = foo().toArray()
    assert([1,2,3,31,32,33,4,5],seq)
}

func yieldManyTest3() { //#96
    func zoo() {
        yield 31
        yield 32
    }
    func bar() {
        yield 21
        yield 22
    }
    func foo() {
        yield 1
        yield 2
        yield many bar()
        yield 3
        yield many zoo()
        yield 4
    }
    var seq = foo().toArray()
    assert([1,2,21,22,3,31,32,4],seq)
}

func yieldManyTest4() { //#96
    func zoo() {
        yield 31
        yield 32
    }
    func bar() {
        yield 21
        yield 22
    }
    func foo() {
        yield 1
        yield 2
        yield many bar()
        yield 3
        yield many zoo()
        yield 4
        yield many bar()
        yield many zoo()
    }
    var seq = foo().toArray()
    assert([1,2,21,22,3,31,32,4,21,22,31,32],seq)
}

func yieldManyTest5() { //#96
    func bar() {
        yield 2
        yield 3
    }
    func foo() {
        yield 1
        yield bar()
    }
    var arr = foo().toArray()
    assert(1, arr[0])
    assert([2,3], arr[1].toArray())
}

func takeTest1() {
    var seq = yields { 1,2,3,4,5 }
    var xs = seq.take(3).toArray()
    assert([1,2,3], xs)
}

func skipTest1() {
    var seq = yields { 1,2,3,4,5 }
    var xs = seq.skip(3).toArray()
    assert([4,5], xs)
}

func firstTest1() {
    var seq = yields { 1,2,3,4,5 }
    assert(1, seq.first())
}

func lastTest1() {
    var seq = yields { 1,2,3,4,5 }
    assert(5, seq.last())
}

func tryCatchBug() { //bug #365
    var val = 0
    func iter() {
        try {
            yield 1
            throw "oops"
            yield 2
        } catch e {
            val = 42
        }
    }
    for _ in iter() { }
    assert(42, val)
}

func yieldBreakTest1() { // #399
    func seq() {
    var x = 100
    while x > 0 {
            yield x
            x -= 1
            if x == 87 {
                yield break
            }
        }
    }
    var xs = []
    for x in seq() {
        xs.add(x)
    }
    assert([100, 99, 98, 97, 96, 95, 94, 93, 92, 91, 90, 89, 88], xs)
}

func sliceTest1() { // #409
    var seq = 1..10
    var xs = seq[5..].toArray()
    assert([6, 7, 8, 9, 10], xs)
}

func rangeInIterator() {
    let seq = yields { 1..5 }
    assert([1,2,3,4,5], seq.toArray())
}

func iteratorStateBug() {
    let seq = yields { 1,2,3,4,5 }
    assert(2, seq[1])
    assert(2, seq[1])
    assert([1,2,3], seq.take(3).toArray())
    assert([1,2,3], seq.take(3).toArray())
}

func elementAtAndIndexer() {
    let seq = yields { 1..10 }
    assert(2, seq.element(1))
    assert(2, seq.element(at: 1))
    assert(10, seq.element(9))
    assert(10, seq.element(at: 9))
    assert(9, seq.element(-1))
    assert(2, seq[1])
    assert(2, seq[1])
    assert(10, seq[9])
    assert(10, seq[9])
}

func emptyTest() {
    let seq = Iterator.empty()
    assert(true, seq is Iterator)
    assert(0, seq.len())
    for x in seq {
        assert(true, false)
    }
}

func repeatTest1() {
    func defaultValue() {
        var i = 0
        while true {
            yield i
            i += 1
        }
    }
    assert([0,0,0], Iterator.repeat(0).take(3).toArray())
    assert([3,3,3], Iterator.repeat(()=>3).take(3).toArray())
    assert([0,1,2,3,4], Iterator.repeat(defaultValue()).take(5).toArray())
}

func repeatTest2() {
    func notNil(x) => x != nil
    func all(seq, fn) {
        for x in seq {
            if !fn(x) {
                return false
            }
        }
        return true
    }
    let xs = Iterator.repeat(rnd).take(20)
    assert(true, all(xs, notNil))
    assert(20, xs.len())
}

func stateIterTest() {
    func it() {
        yield 1
        yield 2
        yield 3
        yield 4
        yield 5
    }
    let xs = yields { 1, 2, 3, 4, 5 }
    let ys = []
    for x in xs {
        ys.add(x)
        if x > 3 {
            break
        }
    }
    assert([1,2,3,4], ys)
    ys.clear()
    for x in xs {
        ys.add(x)
    }
    assert([1,2,3,4,5], ys)
}

func reverseTest() {
    let xs = [1, 2, 3, 4, 5]
    assert([5, 4, 3, 2, 1], xs.iter().reverse().toArray())
    let ys = yields { 'a', "str", 12 }
    assert([12, "str", 'a'], ys.reverse().toArray())
}

func sortTest1() {
    let refs = yields {3,5,1,6,2,4}
    let xs = refs.sort().toArray()
    assert([1,2,3,4,5,6], xs)
    let ys = refs.sort((x,y) => y - x).toArray()
    assert([6,5,4,3,2,1], ys)
}

func sortTest2() {
    var refs = yields {3,5,0,1,2,4}
    try {
        refs = refs.sort((x,y) => x / y)
    } catch {
        DivisionByZero() => assert([3,5,0,1,2,3], refs.toArray())
    }
}

func shuffleTest() {
    func eq(xs, ys) {
        return false when xs.len() != ys.len()
        for i in 0..<xs.len() {
            if xs[i] != ys[i] {
                return false
            }
        }
        return true
    }
    let xs = yields { 1,2,3,4,5,6 }
    var ys = xs.shuffle().toArray()
    assert(6, ys.len())
    assert(true, !eq(xs.toArray(), ys))
}

func countTest() {
    let refs = yields {3,5,1,6,2,4}
    let c = refs.count(x => x % 2 == 0)
    assert(3, c)
}

func mapTest1() {
    let xs = yields { 1,2,3,4,5 }
    let ys = xs.map(x => x + x).toArray();
    assert([2,4,6,8,10], ys)
    let ys1 = xs.map(transform: x => "s" + x).toArray();
    assert(["s1","s2","s3","s4","s5"], ys1)
}

func mapTest2() {
    func iter(x) {
        yield 1
        yield 2
        yield 3
        yield 4 / x
        yield 5
    }
    let xs = iter(4)
    let ys = xs.map(x => x + x).toArray()
    assert([2,4,6,2,10], ys)
    let xs1 = iter(0)
    let res = []
    try {
        for x in xs1.map(x => x + x) {
            res.add(x)
        }
    } catch {
        DivideByZero() => assert([2,4,6], res)
    }
}

func filterTest() {
    let xs = { 1..10 }
    let ys = xs.filter(include: x => x > 5)
    assert([6,7,8,9,10], ys.toArray())
}

func takeWhileTest() {
    let xs = { 1..10 }
    let ys = xs.takeWhile(take: x => x < 6)
    assert([1,2,3,4,5], ys.toArray())
}

func skipWhileTest() {
    let xs = { 1..10 }
    let ys = xs.skipWhile(skip: x => x < 6)
    assert([6,7,8,9,10], ys.toArray())
}

func reduceTest() {
    let xs = { 1..10 }
    var res = xs.reduce(calculate: (x,y) => x + y)
    assert(55, res)
    res = xs.reduce(10, (x,y) => x + y)
    assert(65, res)
}

func toMapTest1() {
    let xs = yields {
        (key: 1, value: "Foo"),
        (key: 2, value: "Bar"),
        (key: 3, value: "Zoo")
    }
    var map = xs.toMap(kv => kv::key, kv => kv::value)
    assert("Foo", map[1])
    assert("Bar", map[2])
    assert("Zoo", map[3])
    map = xs.toMap(kv => kv::key)
    assert((key: 1, value: "Foo"), map[1])
    assert((key: 2, value: "Bar"), map[2])
    assert((key: 3, value: "Zoo"), map[3])
}

func toMapTest2() {
    let xs = yields {
        (key: 1, value: "Foo"),
        (key: 2, value: "Bar"),
        (key: 1, value: "Zoo")
    }
    try {
        let _ = xs.toMap(kv => kv::key)
        assert(true, false)
    } catch {
        KeyAlreadyPresent() => assert(true, true)
    }
}

func emptySequence() {
    func iter() {
        yield break
    }
    var xs = iter()
    assert(0, xs.len())
    assert(nil, xs.first())
    assert(nil, xs.last())
    assert([], xs.toArray())
    xs = yields {}
    assert(0, xs.len())
    assert(nil, xs.first())
    assert(nil, xs.last())
    assert([], xs.toArray())
}