//Done
func tailRecursion1() {
    var xs = []
    func loop(n, max) {
        func loop(i) {
            if i < max {
                xs.add(i)
                loop(i + 1)
            }
        }
        loop(n)
    }
    loop(0, 10)
    assert([0, 1, 2, 3, 4, 5, 6, 7, 8, 9], xs)
}

func tailRecursion2() {
    func foo(a, b) {
        func foo(a, b, c) {
            if a + b == 0 {
                c
            } else {
                var x = caller()(a + b, -(a + b))
                foo(x, -x, x + a + b - c)
            }
        }
        if a + b == 0 {
            42
        } else {
            foo(a, b, a + b)
        }
    }
    var res = foo(1, 2)
    assert(42, res)
}

func tailRecursion3() {
    func withpm(obj) {
        match obj {
            (x, y) => withpm(42),
            (x, y, z) => withpm(84),
            42 => "foo",
            84 => "bar"
        }
    }
    var res = withpm((1,2))
    assert(res, "foo")
}

func recursionTest1() { //#62
    var acc = 0
    func iter(n) {
        if n == 0 {
            0
        } else {
            acc = acc + 1;
            iter(n - 1)
        }
    }
    iter(10)
    assert(acc, 10)
}

func recursionTest2() { //#62
    func foo(x, y) {
        var foo = (x, y) => x - y
        foo(x, y)
    }
    var res = foo(12, 2)
    assert(res, 10)
}


func functionTest1() {
    func sum(x,y) {
        if y == 2 {
            x + y
        } else {
            var z = sum(y, 2)
            x -= 1
            x + z
        }
    }
    var res = sum(2, 4)
    assert(7, res)
}

func functionTest2() {
    func test1(x, y, z) {
        (x, y, z)
    }
    var res = test1(z: 12, x: 3, y: 1)
    assert((3, 1, 12), res)
}

func functionTest3() {
    func test1(x, x1 = 144, y, z, z1 = 33) {
        (x, x1, y, z, z1)
    }
    var res = test1(z: 12, x: 3, y: 1)
    assert((3, 144, 1, 12, 33), res)
}

func functionTest4() {
    func test1(args..., separator = ",", terminator = "\n") {
        [args, separator, terminator]
    }
    assert( [(1,2), ",", "\n"], test1(1,2) )
    assert( [(1,2), ",", "\r"], test1(1,2, terminator: "\r") )
    assert( [(1,2), ";", "\r"], test1(1,2, terminator: "\r", separator: ";") )
}

func functionTest5() {
    func test1(optional = "foo") {
        optional
    }
    assert("foo", test1())
    var arg = "bar"
    assert(arg, test1(arg))
}

func functionTest6() {
    func apply(fun, fst) { snd => fun(fst, snd) }
    func sum(x, y) { x + y }
    var sum2 = apply(sum, 2)
    assert(5, sum2(3))
}

func functionTest7() {
    func apply(fun, fst) { snd => fun(fst, snd) }
    func flip(fun) { (y, x) => fun(x, y) }
    func sub(x, y) { x - y }
    var sub3 = apply(flip(sub), 3)
    assert(6, sub3(9))
}

func functionTest8() {
    var res
    func variadic(args...) {
        res = args.len()
    }
    variadic(args: (1,2,3))
    assert(3, res)
}

func functionTest9() {
    var res
    func variadic(args...) {
        res = args.len()
    }
    variadic([1,2,3,4])
    assert(1, res)
}

func functionTest10() {
    var f = x => x
    assert(12, f(12))
}

func functionTest11() {
    var f = $0 ?? 22
    assert(12, f(12))
    assert(22, f(0))
}

func functionTest12() {
    func foo() {
        assert("foo", current().name)
        assert("bar", caller().name)
    }
    func bar() {
        foo()
    }
    bar()
}

func functionTest13() {
    assert(functionTest13.name, current().name)
    assert(nil, caller())
}

func functionTest14() {
    func thy() { caller() }
    func snd() { thy() }
    func fst() { snd() }
    var s = fst()
    assert("snd", s.name)
}

func functionTest15() {
    let f = x => x * x
    assert("<func>", f.name)
}

func functionTest16() {
    func foo() {
        return "\(current().name) is called by \(caller().name)"
    }
    func bar() {
        foo()
    }
    var s = bar()
    assert("foo is called by bar", s)
}

func functionTest17() { //#294
    $0 + $1
    assert(true, true)
}

func variadicTest1() {
    func takeItAll(x, y, args...) {
        var str = "x=" + y + ";y=" + y + ";other=["
        var count = 0
        for x in args {
            if count > 1 {
                str += ","
            }
            str += x
            count += 1
        }
        str += "]"
        str
    }
    var x = takeItAll(12, 33.4, "oops", true, [1], (x: 0.33, y: 1))
    assert("x=33.4;y=33.4;other=[oopstrue,[1],(x: 0.33, y: 1)]", x)
}

func variadicTest2() {
    func take(args...) {
        args.toString()
    }
    var x = take(1,2,44.42)
    assert("(1, 2, 44.42)", x)
}

func variadicTest3() {
    func take(x, args...) {
        var str = "x=" + x
        str += args
        str
    }
    var x = take(14)
    assert("x=14()", x)
}

func complexToStringTest1() {
    func Integer.toString() {
        if this % 2 == 0 {
            "even"
        } else {
            "odd"
        }
    }
    assert("even", 12.toString())
    Integer.__deleteMember("toString")
}

func complexToStringTest2() {
    func Nil.toString() {
        "null"
    }
    var x = "foo" + nil
    assert("foonull", x)
    Nil.__deleteMember("toString")
}

func methodTest1() {
    func Integer +(other) {
        this * other
    }
    var res = 3 + 3 == 3 * 3
    Integer.__deleteMember("op_add")
    assert(true, res)
}

func methodTest2() {
    func Float +(other) {
        this.toString() + " + " + other.toString()
    }
    var res = 12.2 + 0.4
    Float.__deleteMember("op_add")
    assert("12.2 + 0.4", res)
}

func methodTest3() {
    var x = 11
    func Integer.getItem(index) { index }
    assert(11, 12[x])
    Integer.__deleteMember("op_get")
}

func closureTest() {
    var xs = []
    let num = 10
    for n in 0..<num {
        var elem = x => () => x * x
        xs.add(elem(n))
    }
    var ys = []
    for x in xs {
        ys.add(x())
    }
    assert([0, 1, 4, 9, 16, 25, 36, 49, 64, 81], ys)
}

func paramsTest1() {
    func pars(x, y) {
        "x=\(x),y=\(y)"
    }
    let res = pars(y: 2, x: 1)
    assert("x=1,y=2", res)
}

func paramsTest2() {
    func getter(x = 12, y = 3, z = 4, j = 44, k = 42) {
        x + y + z + j + k
    }
    assert(60, getter(3, 4, k: 5))
    assert(62, getter(j: 1))
    assert(105, getter())
}

func paramsTest3() {
    func pars(x = 1, y = 2, z = 3) {
        "x=\(x),y=\(y),z=\(z)"
    }
    var res = pars(4)
    assert("x=4,y=2,z=3", res)
    res = pars(x: 4)
    assert("x=4,y=2,z=3", res)
    res = pars(z: 4)
    assert("x=1,y=2,z=4", res)
    res = pars(10, 20)
    assert("x=10,y=20,z=3", res)
}

func paramsTest4() {
    func pars(x, y, z) { }
    let res = try {
        pars(1, 2, x: 3)
    } catch {
        MultipleValuesForArgument(funName, argName) => "\(funName)->\(argName)"
    }
    assert("pars->x", res)
}

func paramerlessLambda() {
    let fn = => 2 + 2
    assert(4, fn())
    func takeIt(fn) {
        return fn()
    }
    let res = takeIt(=> { 22 + 22 })
    assert(44, res)
}

func introspectionTest1() {
    func tt(x, y, z = 3, extras...) {}
    assert("tt", tt.name)
    assert(4, tt.parameters.len())
    var i = 0
    for p in tt.parameters {
        if i == 0 {
            assert("(name: x, hasDefault: false, default: nil, varArg: false)", p.toString())
        } else if i == 1 {
            assert("(name: y, hasDefault: false, default: nil, varArg: false)", p.toString())
        } else if i == 2 {
            assert("(name: z, hasDefault: true, default: 3, varArg: false)", p.toString())
        } else if i == 3 {
            assert("(name: extras, hasDefault: false, default: nil, varArg: true)", p.toString())
        }
        i += 1
    }
}

func exprBody1() {
    func sum(x, y) => x + y
    assert(5, sum(2, 3))
    func sum3(x, y, z) => {
        x + y + z
    }
    assert(9, sum3(2, 3, 4))
}

func funcWithDefaults1() {
    func double(x = 2) => x + x
    func sum2(x, y = 3) => x + y
    func sum3(x = 1, y = 0.5, z = 0.25) => x + y + z
    assert(4, __invoke(double))
    assert(6.33, __invoke(sum2, 3.33))
    assert(1.75, __invoke(sum3))
}

func funcWithDefaults2() {
    assert(12.12, __invoke(round, 12.1245677))
    let r = __invoke(rnd, 10)
    assert(true, r >= 10)
    assert(true, __invoke(rnd) >= 0)
    let r2 = __invoke(rnd, 1, 5)
    assert(true, r2 >= 1 && r2 <= 5)
}

func funcWithDefaults3() {
    var cell
    func collector(args..., x = 5) {
        var xs = [x]
        xs.addRange(args)
        cell = xs
    }
    __invoke(collector, 4)
    assert([5, 4], cell)
}

func funcWithDefaults4() {
    var cell
    func collector(x, y, args...) {
        var xs = [x, y]
        xs.addRange(args)
        cell = xs
    }
    __invoke(collector, 1, 2, (3, 4))
    assert([1, 2, 3, 4], cell)
}

func funcWithDefaults5() {
    var cell
    func collector(x, y, args...) {
        var xs = [x, y]
        xs.addRange(args)
        cell = xs
    }
    __invoke(collector, 5, 6, [7, 8])
    assert([5, 6, 7, 8], cell)
}

func externalCall() {
    func checkIt() {
        assert(nil, caller())
    }
    __invoke(checkIt)
}

func funcTypeAnnotations1() {
    func sum(Integer x, Integer y) {
        x + y
    }
    assert(12, sum(10, 2))
    try {
        sum(12.2, 0)
        assert(got: false)
    } catch {
        InvalidType() => assert(got: true)
    }
}

import lib = refs/libtest1
func funcTypeAnnotations2() {
    func assetToString(lib::Asset ass) {
        ass.toString()
    }
    assert("Asset.Phone(model: 1+, memory: 8)", assetToString(Asset.Phone("1+", 8)))
    try {
        assetToString(12)
        assert(got: false)
    } catch {
        InvalidType() => assert(got: true)
    }
}

func mutualRecursion1() {
    func f(x) {
        g(x - 1) when x > 0 else x
    }
    and g(x) {
        f(x - 1) when x > 0 else x
    }
    assert(0, f(1))
    assert(-1, f(-1))
}

func mutualRecursion2() {
    func f(n) {
        n == 0 ? 1 : n - m(f(n-1))
    }
    and m(n) {
        n == 0 ? 0 : n - f(m(n-1))
    }
    let xs = (0..20).map(i => f(i)).toArray()
    let ys = (0..20).map(i => m(i)).toArray()
    assert([1, 1, 2, 2, 3, 3, 4, 5, 5, 6, 6, 7, 8, 8, 9, 9, 10, 11, 11, 12, 13], xs)
    assert([0, 0, 1, 2, 2, 3, 4, 4, 5, 6, 6, 7, 7, 8, 9, 9, 10, 11, 11, 12, 12], ys)
}