#region "Type info"
    lazy x = "a" + "b" + "c"
    assert(String, x.GetType())
    lazy y = 1 + 2 + 3
    assert(Integer, y.GetType())
    assert(Integer, y.GetType())
    lazy z = 1.0 + 2.0 + 3.0
    assert(got: Float == z.GetType())
#endregion

#region "Simple 1"
    lazy x = 2 + 2
    let y = x + x
    assert(8, y)
#endregion

#region "Simple 2"
    var it = 0
    lazy x = {
        it += 1
        2 + it
    }
    let y = x + x
    assert(6, y)
#endregion

#region "Simple 3"
    let xs = []
    lazy x = {
        xs.Add(42)
        2 + 2
    }
    assert(got: xs is not [42])
    assert(6.5, x + 2.5)
    assert(4, x)
    assert(got: xs is [42])
#endregion

#region "Methods: ToString"
    let x = 2 + 2
    assert("4", x.ToString())
    let y = {
        let v = 4
        let w = 6
        "\(v)+\(w)=\(v+w)"
    }
    assert("4+6=10", y.ToString())
#endregion

#region "Methods: IsNaN"
    lazy d = Float.Inf() + (-Float.Inf())
    assert(true, d.IsNaN())
#endregion

#region "Conversion to boolean"
    lazy x = 2
    if x {
        assert(got: true)
    } else {
        assert(got: false)
    }
    lazy y = nil
    if y {
        assert(got: false)
    } else {
        assert(got: true)
    }
#endregion

#region "Math"
    lazy x = 12 / 3
    lazy y = 2 + 3
    lazy z = 12
    assert(9, x + y)
    assert(60, z * 5)
    assert(3, x - 1)
    lazy x1 = 44 - 40
    lazy y1 = 2 + 0
    assert(2, x1 / y1)
    assert(0, x1 % y1)
#endregion

#region "Invoke members"
    lazy x = 2 + 13
    assert(got: x.IsMultiple(of: 5))
    assert(got: x.IsMultiple(of: 3))
    lazy str = "Hello" + ", " + "world"
    assert("Helloworld", str.Replace(", ", ""))
    lazy c = 'A'
    assert(got: c.IsUpper())
#endregion

#region "Lazy arrays"
    lazy xs = {
        let xs = []
        for i in 1..10 {
            xs.Add(i)
        }
        xs
    }
    xs.Add(11)
    assert([1,2,3,4,5,6,7,8,9,10,11], xs)
    assert(11, xs.Length())
    xs.RemoveRange([1,2,9,10,11])
    assert([3,4,5,6,7,8], xs)
    xs.Remove(8)
    assert([3,4,5,6,7], xs)
    xs.RemoveAt(0)
    assert([4,5,6,7], xs)
    xs[1] = 55
    assert([4,55,6,7], xs)
#endregion

#region "Lazy tuples"
    lazy xs = {
        (1,) + (2, 3)
    }
    assert((1,2,3,4), xs.Add(4))
    assert(1, xs.First())
    assert(2, xs.Second())
    assert(3, xs.Length())
#endregion

#region "Lazy strings"
    lazy s1 = "one" + "," + "two"
    lazy s2 = "," + "three"
    assert(",two", s1.Substring(3))
    assert("one,two,three", s1 + s2)
    lazy s3 = "'val'"
    assert("val", s3.Trim('\''))
#endregion

#region "Lazy custom type"
    type Foo()
    func Foo.Call() {
        "FromFoo!"
    }
    func Foo.ToString() {
        "Foo.ToString"
    }
    lazy x = {
        Foo()
    }
    assert("FromFoo!", x())
    assert("Foo.ToString", x.ToString())
#endregion

#region "Lazy and operator overloading"
    func Integer + (other) => this.ToString() + other.ToString()
    lazy x = 12
    assert("122", x + 2)
#endregion

#region "Lazy indexers"
    let xs = [1,2,3]
    type Foo()
    func get Foo[index] => index * 10
    func set Foo[index, value] => xs[index] = value
    lazy x = Foo()
    assert(30, x[3])
    x[1] = 22
    assert(22, xs[1])
#endregion

#region "Lazy properties"
    type Foo()
    func get Foo.Bar() { "Bar!" }
    lazy x = Foo()
    assert("Bar!", x.Bar)
#endregion

#region "Lazy exceptions"
    let xs = []
    lazy x = {
        xs.AddRange(yields { 1, 2 })
        throw Unknown(xs)
    }
    try {
        let _ = x.ToString()
    } catch {
        @Unknown(xs) => assert([1, 2], xs)
    }
#endregion

#region "Convert lazy"
    type Foo()
    func Foo as Integer { 42 }
    lazy f = Foo()
    assert(42, f as Integer)
#endregion

#region "Overloaded operators"
    func Integer + (other) => this.ToString() + other
    lazy x = 2 + 2
    assert("221", x + 1)
#endregion