//Done
func mathTest1() {
    var x = 12 * 3 - 4 + 6
    assert(38, x)
#endtest

func mathTest2() { //1.5919117647058822
    var x = 22 % 4 - 3.33 / 8.16
    assert(1.5919117647058822, x)
#endtest

func mathTest3() { //6.6129032258064528
    var x = 12.3 / (0.64 + 1.22)
    assert(6.6129032258064528, x)
#endtest

func mathTest4() { //11.256
    var x = 23.456 % 12.2
    assert(11.256, x)
#endtest

func mathTest5() { //0.0099999999999997868
   var x = 12.4 - 0.39 - 12.0
   assert(0.0099999999999997868, x)
#endtest

func mathTest6() { //783.59999999999991
   var x = 12.0 * 65.3
   assert(783.59999999999991, x)
#endtest

func mathTest7() {
    var x = 188.123 - 12.0004 * 5.667 - (12.2 / 0.134)
    assert(29.071957080597016, x)
#endtest

func mathTest8() {
    var x = -12 - 14.6
    assert(-26.6, x)
#endtest

func mathTest9() {
    var x = 199 % 44.12 * 2
    assert(45.04000000000002, x)
#endtest

func mathTest10() {
    var x = +16 - -22
    assert(38, x)
#endtest

func mathTest12() {
    var x = 2.0 / 0
    assert(true, x == Float.inf())
#endtest

func mathTest13() {
    var x = .0 / 0
    assert(true, x.isNaN())
#endtest

func logicalTest1() {
    let x = 144
    var res = x / 12 > 100 || x / 6 == 24
    assert(true, res)
#endtest

func logicalTest2() {
    let x = 144
    var res = x / 12 > 100 && x / 6 != 24
    assert(false, res)
#endtest

func comparisonTest1() {
    var res = 12 > 11 && 12 <= 12 && 12 != 12.01 && 12 >= 12
    assert(true, res)
#endtest

func comparisonTest2() {
    assert(true, "string1" != "string2" && "string1" == "string1")
#endtest

func comparisonTest3() {
    let x = 12
    let y = 9
    assert(false, !(x >= 12 && y <= 10 || x < -12))
#endtest

func comparisonTest4() {
    assert(true, 12.0 == 12)
    assert(true, 12 == 12.0)
#endtest

func comparisonTest5() {
    assert(true, 12.2 > 12)
    assert(true, 12 < 12.2)
#endtest

func comparisonTest6() {
    var str1 = "x"
    var str2 = ""
    assert(true, str1 == true)
    assert(false, str2 == true)
    assert(true, str2 == false)
#endtest

func comparisonTest7() {
    assert(true, "".toString == "".toString)
    assert(false, "".toString == '\0'.toString)
#endtest

func coalesceTest1() {
    var z = nil
    var foo = "foo"
    var obj = false
    assert("foo", z ?? foo)
    assert("foo", obj ?? z ?? foo)
#endtest

func coalesceTest2() { //#306
    var x = 2
    var y
    x ??= 3
    y ??= 133
    assert((2, 133),(x,y))
#endtest

func toStringTest1() {
    var res = (1,2,3).toString()
    assert("(1, 2, 3)", res)
#endtest

func toStringTest2() {
    var res = [12.2, "string", true].toString()
    assert("[12.2, string, true]", res)
#endtest

func toStringTest3() {
    var res = (x: 42,y: "foo").toString()
    assert("(x: 42, y: foo)", res)
#endtest

func toStringTest4() {
    func foo(args..., quotes = 12) { }
    let pat = "foo(args..., quotes = 12)";
    assert(pat, foo.toString())
#endtest

func baseTest1() {
    let x = 2
    func inner() {
        let x = 4
        base.x
    }
    assert(2, inner())
#endtest

func baseTest2() {
    let x = 2
    func inner() {
        let x = 4
        if x == 4 {
            var x = 8
            base.x
        }
    }
    assert(2, inner())
#endtest

func floatLiteralTest() {
    var res = 1.234e-17
    assert(0.00000000000000001234, res)
#endtest

func integerLiteralTest() {
    var res = 0x40 + 0x20
    assert(96, res)
#endtest

func memberCheckTest1() {
    assert(true, "string" is .len)
    assert(false, 42 is .fooBar)
#endtest

func memberCheckTest2() { //#270
    assert(true, String is .join)
    assert(false, String is .foo)
    assert(true, "" is .split)
    assert(false, "" is .join)
#endtest

func memberCheckTest3() { //#271
    var nm = "split"
    assert(true, "".has(nm))
    assert(true, "" is .split)
    assert(true, "".has("split"))
#endtest

func memberCheckTest4() { //#272
    assert(true, String.has("join"))
    assert(false, String.has("split"))
    assert(true, String.has("id"))
    assert(true, String.has("name"))
#endtest

func nameofTest() {
    assert("nameofTest", nameof(nameofTest))
    assert("toString", nameof("".toString))
    var tup = (x: 12)
    assert("x", nameof(tup.x))
#endtest

func parseTest() {
    var res = parse("[1,2,3,('c',true,23.4),nil]")
    assert([1,2,3,('c',true,23.4),nil], res)
#endtest

func cloneTest() {
    var xs = [1,2,3]
    assert(true, xs == xs)
    assert(false, xs.clone() == xs)
#endtest

func compositionTest1() {
    func sum(x, y) {
        x + y
    }
    func fmt(res) {
        "result=\(res)"
    }
    let fun = Function.compose(sum, fmt)
    var res = fun(13, 14)
    assert(res, "result=27")
#endtest

func compositionTest2() {
    func sum(x, y) {
        x + y
    }
    func fmt(res) {
        "result=\(res)"
    }
    let fun = sum.compose(with: fmt)
    var res = fun(13, 14)
    assert(res, "result=27")
#endtest

func getTypeTest1() {
    assert(true, 42.getType() == Integer)
    assert(true, "".getType() == String)
    assert(true, 42.01.getType() == Float)
    assert(true, '\s'.getType() == Char)
#endtest

func getTypeTest2() {
    var x = 42
    assert(true, x.getType() == Integer)
    var t = x.getType()
    assert(1, t.id)
    assert("Integer", t.name)
#endtest

func getTypeTest3() {
    var x = 42
    assert(true, TypeInfo(x) == Integer)
    var t = TypeInfo(x)
    assert(1, t.id)
    assert("Integer", t.name)
#endtest

func hasTest1() {
    var t = (1,2,3)
    assert(false, t.has("foo"))
    var foo = "foo"
    assert(false, t.has(foo))
    assert(true, t.has("keys"))
    var keys = "keys"
    assert(true, t.has(keys))
#endtest

func hasTest2() {
    var t = [1,2,3]
    assert(false, t.has("foo"))
    var foo = "foo"
    assert(false, t.has(foo))
    assert(true, t.has("indices"))
    var indices = "indices"
    assert(true, t.has(indices))
#endtest

func typeConstantsTest1() {
    assert(9223372036854775807, Integer.max())
    assert(-9223372036854775807, Integer.min() + 1)
#endtest

func typeConstantsTest2() {
    assert(1.7976931348623157E+308, Float.max())
    assert(-1.7976931348623157E+308, Float.min())
    var d = Float.inf() + (-Float.inf())
    assert(true, d.isNaN())
#endtest

func typeConstantsTest3() {
    assert('\0', Char.min())
    assert('\uffff', Char.max())
#endtest

func floatTest1() {
    assert(12.2, Float(12.2))
    assert(12.0, Float(12))
    assert(5.0, Float('5'))
#endtest

func floatTest2() {
    assert(0.0, Float.default())
#endtest

func integerTest1() {
    assert(12, Integer(12.2))
    assert(1222, Integer(1222))
    assert(5, Integer('5'))
#endtest

func integerTest2() {
    assert(0, Integer.default())
#endtest

func nilTest1() {
    var x
    assert(nil, x)
    assert(nil, Nil())
    assert(true, nil == nil)
#endtest

func nilTest2() {
    assert(nil, Nil.default())
#endtest

func boolTest1() {
    assert(true, Bool(12))
    assert(false, Bool(0))
    assert(true, Bool("str"))
    assert(false, Bool(""))
    assert(false, Bool(nil))
#endtest

func boolTest2() {
    assert(false, Bool.default())
#endtest
////////////////////////////////////////////////////////////

func guardsTest1() {
    func safeDiv(x, y) {
        return -1 when y == 0
        return x / y
    }
    var x = safeDiv(4, 2)
    var y = safeDiv(2, 0)
    assert(2, x)
    assert(-1, y)
#endtest

func guardsTest2() {
    var x = 0
    x += 1 when x < 1
    assert(1, x)
#endtest

func guardsTest3() {
    var x = 0
    x += 1 when x < 0
    assert(0, x)
#endtest

func guardsTest4() {
    func safeDiv(x, y) {
        return when y == 0
        return x / y
    }
    var res = safeDiv(10, 0)
    assert(nil, res)
#endtest

func guardsTest5() {
    func safeDiv(x, y) {
        0 when y is 0 else x / y
    }
    assert(5, safeDiv(10, 2))
    assert(0, safeDiv(10, 0))
#endtest

func guardsTest6() {
    let (x, y) = (1, 2)
    set (x, y) = (12, 14) when x > 0 && y > 1
    assert((12, 14), (x, y))
#endtest

func guardsTest7() {
    let (x, y) = (1, 2)
    var res
    if x < 2 { res = "x < 2" } when y > 10 else { res = "y < 10" }
    assert("y < 10", res)
#endtest

func guardsTest8() {
    func fib(n) =>
        n when n < 2 else fib(n - 1) + fib(n - 2)
    assert(89, fib(11))
#endtest

func guardsTest9() {
    var x = 1
    var y = 42
    x = y when y > 42 else y = 84
    assert(1, x)
    assert(84, y)
#endtest

func returnTest() {//Issue 266
    func foo() {
        return 2
    }
    func bar() {
        return
        22
    }
    assert(2, foo())
    assert(nil, bar())
#endtest

func evalTest() { //Issue 269 --> DONE
    var z = eval("x + y", (x: 2, y: 3) )
    assert(5, z)
#endtest

func intRangeTest1() {//--> DONE
    assert([0,1,2,3,4,5], [0..5])
    assert([0,2,4], (0^2..4).toArray())
#endtest

func floatRangeTest1() {//--> DONE
    assert([0.0,1.0,2.0,3.0,4.0,5.0], [0.0..5])
    assert([0.0,0.02,0.04], (0.0^0.02..0.05).toArray())
#endtest

func sumTest() { //--> DONE
    var x = 2
        + 3
        + 5
        + 6
    assert(16, x)
    var y = 12
        + 
        -44
    assert(-32, y)
    var z = 12
        + -3
        --12
        -44
    assert(-23, z)
#endtest

func isMultipleTest() { //--> DONE
    assert(true, 12.isMultiple(3))
    assert(true, 12.isMultiple(of: 2))
    assert(true, 340.isMultiple(of: 10))
    assert(false, 340.isMultiple(of: 13))
#endtest

func intParse() { //--> DONE
    assert(42, Integer.parse("42"))
    assert(2, Integer.parse('2'))
    assert(nil, Integer.parse("three"))
#endtest

func floatParse() { //--> DONE
    assert(42.42, Float.parse("42.42"))
    assert(2.0, Float.parse('2'))
    assert(nil, Float.parse("three and a half"))
#endtest

func ternaryTest1() { //--> DONE
    var x = 0
    var res = x > 0 ? x
        : x < 0 ? -x : 42
    assert(42, res)
#endtest

func ternaryTest2() { //--> DONE
    func fib(n) =>
        n < 2 ? n : fib(n - 1) + fib(n - 2)
    assert(89, fib(11))
#endtest

func unaryTest1() { //--> DONE
    let x = - 42 //with space
    assert(0 - 42, x)
#endtest

func inTest1() { //--> DONE
    var xs = [1..10]
    assert(true, 1 in xs)
    assert(true, 10 in xs)
    assert(false, 101 in xs)
    xs = (1..10)
    assert(true, 1 in xs)
    assert(true, 10 in xs)
    assert(false, 101 in xs)
    xs = yields {1..10}
    assert(true, 1 in xs)
    assert(true, 10 in xs)
    assert(false, 101 in xs)
#endtest

func inTest2() { //--> DONE
    var xs = (x: 12, y: 24)
    assert(true, "x" in xs)
    assert(true, "y" in xs)
    assert(false, "z" in xs)
    xs = Dictionary.fromTuple(xs)
    assert(true, "x" in xs)
    assert(true, "y" in xs)
    assert(false, "z" in xs)
#endtest

func typeErrorTest() { //--> DONE
    try {
        12 + '2'
        assert(got: false)
    } catch {
        InvalidType() => assert(got: true)
    }
#endtest

func propertyExtTest() { //--> DONE
    func get Array.first() { 
        this[0] when this.len() > 0
    }
    func set Array.first(val) {
        this.add(val) when this.len() == 0 else this[0] = val
    }
    let xs = [1,2,3]
    assert(1, xs.first)
    xs.first = 11
    assert(11, xs.first)
    Array.__deleteMember("first")
    Array.__deleteMember("set_first")
#endtest