#test "Constructor"
    let xs = Dictionary(
        (x: 1, y: 2, z: "foo")
    )
    assert(1, xs["x"])
    assert(2, xs["y"])
    assert("foo", xs["z"])
#endtest

#test "Methods: getType"
    let x = Dictionary()
    assert(true, TypeInfo(x) == Dictionary)
    assert(true, x.getType() == Dictionary)
    var t = TypeInfo(x)
    assert(13, t.id)
    assert("Dictionary", t.name)
#endtest

#test "Methods: clone"
    let xs = Dictionary()
    assert(true, xs == xs)
    assert(false, xs.clone() == xs)
#endtest

#test "Methods: fromTuple"
    var map = Dictionary.fromTuple( (name: "John", surname: "Doe", age: 50) )
    assert("John", map::name)
    assert("Doe", map::surname)
    assert(50, map::age)

    map = Dictionary.fromTuple( (foo: "foo", bar: "bar") )
    assert("foo", map::foo)
    assert("bar", map::bar)
#endtest

#test "Methods: add"
    var map = Dictionary()
    map.add("x", 1)
    map.add("y", 2)
    assert(1, map["x"])
    assert(2, map["y"])

    map = Dictionary()
    map.add(("x",1), 1)
    map.add(("y",2), 2)
    assert(1, map[("x",1)])
    assert(2, map[("y",2)])
#endtest

#test "Indexing"
    var map = Dictionary()
    map[42] = 420
    map[42] = map[42] * 2
    assert(840, map[42])
#endtest

#test "Methods: tryAdd, tryGet"
    var map = Dictionary()
    map.add(12, 12)
    if !map.tryAdd(12, 12) {
        map.remove(12)
    }
    var res = map.tryGet(12)
    assert(nil, res)
#endtest

#test "Methods: len, clear"
    var map = Dictionary()
    map.add("x", 1)
    map["x"] = 2
    map["y"] = 3
    assert(2, map.len())
    map.clear()
    assert(0, map.len())
#endtest

#test "Complex keys"
    var map = Dictionary()
    map[[1,2,3]] = [4,5,6]
    assert([4,5,6], map[[1,2,3]])
#endtest

#test "Field access"
    var map = Dictionary()
    map::name = "John"
    map::surname = "Doe"
    map::age = 50
    assert("John", map::name)
    assert("Doe", map::surname)
    assert(50, map::age)
#endtest

#test "Iterator to dictionary"
    let xs = yields {
        (key: 1, value: "Foo"),
        (key: 2, value: "Bar"),
        (key: 3, value: "Zoo")
    }
    let map = xs.toDictionary(v => v::key, v => v::value)
    for (key,value) in map {
        if key == 1 {
            assert("Foo", value)
        } else if key == 2 {
            assert("Bar", value)
        } else if key == 3 {
            assert("Zoo", value)
        }
    }
#endtest

#test "Methods: compact"
    let xs = Dictionary()
    xs.add("foo", 1)
    xs.add("bar", nil)
    xs.add("zoo", 2)
    var res = xs.compact()
    assert(2, res.len())
    assert(1, res::foo)
    assert(2, res::zoo)
    res = xs.compact(v => {
        if !v {
            42
        } else if v > 1 {
            v
        }
    })
    assert(2, res.len())
    assert(2, res::zoo)
    assert(42, res::bar)
    let data = Dictionary.fromTuple(("a": "1", "b": "three", "c": "4?"))
    res = data.compact(Integer.parse)
    assert(1, res.len())
    assert(1, res["a"])
#endtest

#test "Methods: toTuple"
    let tuple1 = (foo: "foo", bar: "bar")
    let tuple2 = (foo: "foo", bar: "bar", "$zoo$": 24)
    var map = Dictionary.fromTuple(tuple1)
    var backTuple = map.toTuple()
    assert(tuple1, backTuple)
    map = Dictionary.fromTuple(tuple2)
    backTuple = map.toTuple()
    assert(tuple2, backTuple)
#endtest

#test "Pattern matching"
    var map = Dictionary.fromTuple ((x: 1, y: 2))
    match map {
        (x: xx, y: yy) => {
            assert(1, xx)
            assert(2, yy)
        }
    }
#endtest

#test "Methods: contains"
    var map = Dictionary.fromTuple ((x: 1, y: 2))
    assert(true, map.contains("x"))
    assert(false, map.contains("X"))
    map[(1,2)] = true
    assert(true, map.contains((1,2)))
#endtest
