type SimpleFoo(x)
func simpleTest1() {
    var f = SimpleFoo(42)
    assert(42, f::x)
    assert(42, f[0])
    assert(1, f.len())
    match f {
        SimpleFoo(x) => assert(42, x)
    }
}

type EmptyFoo()
func simpleTest2() {
    var f = EmptyFoo()
    assert(0, f.len())
    match f {
        EmptyFoo() => assert(true, true)
    }
}

type Asset = Computer(model, memory, rank) or Phone(model, memory) or Modem(model)
func simpleTest3() {
    var a1 = Asset.Computer(model: "Lenovo", memory: 16, rank: 3)
    var a2 = Asset.Phone("1+", 8)
    var a3 = Asset.Modem("Zyxel")
    assert(3, a1.len())
    assert(2, a2.len())
    assert(1, a3.len())
    assert("Lenovo"+16+3, a1::model+a1::memory+a1::rank)
    assert("1+"+8, a2::model+a2::memory)
    assert("Zyxel", a3::model)
    assert("Asset.Computer(model: Lenovo, memory: 16, rank: 3)", a1.toString())
    assert("Asset.Phone(model: 1+, memory: 8)", a2.toString())
    assert("Asset.Modem(model: Zyxel)", a3.toString())
}

type Void()
func simpleTest4() {
    var v = Void()
    assert(0, v.len())
    assert("Void()", v.toString())
}

func matchTypeTest1() {
    var a1 = Asset.Computer(model: "Lenovo", memory: 16, rank: 3)
    var a2 = Asset.Phone("1+", 8)
    func mem(a) {
        match a {
            Asset.Computer(model, memory, rank) => memory,
            Asset.Phone(model, memory) => memory
        }
    }
    assert(16, mem(a1))
    assert(8, mem(a2))
}

type Bar = {
    var data
}
func extendTest1() {
    static func Bar.Bar(args...) {
        this.ini.data = args
    }
    func Bar.len() {
        this.ini.data.len()
    }
    func Bar.getItem(index) {
        this.ini.data[index]
    }
    var obj = Bar.Bar(1, 2, 3)
    var res = match obj {
        (x, y) => x + y,
        (x, y, z) => x + y + z
    }
    assert(6, res)
    Bar.__deleteMember("Bar")
    Bar.__deleteMember("len")
    Bar.__deleteMember("getItem")
}

func extendTest2() {
    static func Bar.Bar(args...) {
        this.ini.data = args
    }
    func Bar.len() {
        this.ini.data.len()
    }
    func Bar.getItem(index) {
        this.ini.data[index]
    }
    var obj = Bar(10, 20, 30)
    assert(10, obj[0])
    assert(20, obj[1])
    assert(30, obj[2])
    var res = match obj {
        (x, y) => x + y,
        (x, y, z) => x + y + z
    }
    assert(60, res)
}

type Maybe = None() or Some(x)
func autoConsTest1() {
    var x = Maybe.Some(42)
    var y = Maybe.None()
    func doMatch(x) {
        match x {
            Some(x) => x,
            None() => "None"
        }
    }
    assert(42, doMatch(x))
    assert("None", doMatch(y))
}

type Foo1 = Bar(x, y, z)
func autoConsTest2() {
    var f = Foo1.Bar(12, "foobar", 3)
    var res = match f {
        Bar(x: xx, y: yy, z: zz) => "\(xx),\(yy)",
        Bar(x, y, z) => x + z,
        Bar() => "not quite"
    }
    assert("12,foobar", res)
}

type Point(x, y)
func autoConsTest3() {
    var p = Point(12, 43)
    assert(12, p[0])
    assert(43, p[1])
    assert(12, p::x)
    assert(43, p::y)
}

type Nat = { var val }
func typeTest1() {
    func fetch(x) {
        Nat(x) when x >= 0 else x
    }
    static func Nat.Nat(x) {
        throw "Negative number." when x < 0
        this.ini.val = x
    }
    func Nat.toInteger() {
        this.ini.val
    }
    func Nat + (other) {
        fetch(this.ini.val + other.ini.val)
    }
    func Nat - (other) {
        fetch(this.ini.val - other.ini.val)
    }
    func Nat * (other) {
        fetch(this.ini.val * other.ini.val)
    }
    func Nat / (other) {
        fetch(this.ini.val / other.ini.val)
    }
    func Nat == (other) {
        this.ini.val == other.ini.val
    }
    var n = Nat(12)
    assert(Nat(24), n + Nat(12))
    assert(Nat(2), n - Nat(10))
    assert(-8, n - Nat(20))
    assert(Nat(24), n * Nat(2))
    assert(Nat(6), n / Nat(2))
    assert(Nat(6), n / Nat(2))
}

type Either = Left(x) or Right(x)
func typeTest2() {
    var x = Either.Left(12)
    var y = Either.Right(24)
    func doMatch(obj) {
        match obj {
            Left(x) => x,
            Right(y) => y
        }
    }
    assert(12, doMatch(x))
    assert(24, doMatch(y))
}

type Cell = { var val }
func typeTest3() {
    func ref(value) {
        Cell(value)
    }
    static func Cell.Cell(value) {
        this.ini.val = value
    }
    func Cell.setValue(value) {
        this.ini.val = value
    }
    func Cell.getValue() {
        this.ini.val
    }
    func Cell.getItem(index) {
        if index != 0 {
            throw "Index out of range."
        }
        this.ini.val
    }
    func Cell.len() {
        1
    }
    var c = ref(12)
    assert(12, c.getValue())
    assert(12, match c {
        Cell(x) => x
    })
    c.setValue(24)
    assert(24, c.getValue())
    assert(24, match c {
        Cell(x) => x
    })
}

type List = Cons(x, next) or Empty()
func typeTest4() {
    func List.toString() {
        func walk(xs) {
            match xs {
                Cons(x, Empty()) => x.toString(),
                Cons(x, next) => x.toString() + "," + walk(next),
                Empty() => ""
            }
        }
        return "[" + walk(this) + "]"
    }
    var xs = List.Cons(1, List.Cons(2, List.Cons(3, List.Cons(4, List.Empty()))))
    assert("[1,2,3,4]", xs.toString())
}

func typeTest5() {
    static func List.generate(values...) {
        var xs = List.Empty()
        for x in values {
            xs = List.Cons(x, xs)
        }
        xs
    }
    var xs = List.generate(1,2,3,4)
    assert("[4,3,2,1]", xs.toString())
}

type Zoo1()
func typeTest6() {
    var x = Zoo1()
    func Zoo1.toString() {
        match this {
            Zoo1() => "nil"
        }
    }
    assert("nil", x.toString())
}

type Zeta()
func simpleTypeTest1() {
    var z = Zeta()
    var res = z.len()
    assert(0, res)
}

type Beta(x)
func simpleTypeTest2() {
    var b = Beta(12)
    assert(1, b.len())
    assert(12, b[0])
    assert(12, b["x"])
    assert(12, b::x)
}

type Record = { var fields }
func accessorTest() {
    static func Record.Record(values...) {
        this.ini.fields = values
    }
    func Record.getItem(index) {
        match index {
            "name" => this.ini.fields::name,
            "age" => this.ini.fields::age
        }
    }
    var p = Record(values: (name: "John", age: 33))
    assert("John", p["name"])
    assert(33, p::age)
}

type UserInfo(name,age)
func setterTest1() {
    var u = UserInfo("Jon", 17)
    assert(17, u::age)
    try {
        u::age = 39
        assert(got: false)
    } catch {
        FieldReadOnly() => assert(got: true)
    }
}

type TypeTestZoo(a, b = 12, c = 33)
func defaultsConsTest() {
    var t = TypeTestZoo(1)
    assert(1, t::a)
    assert(12, t::b)
    assert(33, t::c)
    t = TypeTestZoo(1, 2)
    assert(1, t::a)
    assert(2, t::b)
    assert(33, t::c)
    t = TypeTestZoo(1, 2, 3)
    assert(1, t::a)
    assert(2, t::b)
    assert(3, t::c)
}

type Point1 = { var px; var py }
func setterTest2() {
    static func Point1.Point1(x, y) {
        this.ini.px = x
        this.ini.py = y
    }
    func Point1.getItem(index) {
        match index {
            0 or "x" => this.ini.px,
            1 or "y" => this.ini.py,
            _ => throw Error.IndexOutOfRange()
        }
    }
    func Point1.setItem(index, value) {
        match index {
            0 or "x" => this.ini.px = value,
            1 or "y" => this.ini.py = value,
            _ => throw Error.IndexOutOfRange()
        }
    }
    var p = Point1(12, 34)
    p::x += p::x
    p::y += p::y
    assert(24, p::x)
    assert(68, p::y)
}

func complexMemberCheckTest1() {
    var t = (x: 2)
    assert(false, t is .x)
}

func complexMemberCheckTest2() {
    func Integer.foobar() {}
    assert(true, 12 is .foobar)
    Integer.__deleteMember("foobar")
}

type Leto(x)
func complexMemberCheckTest3() {
    func Leto.foobar() {}
    var p = Leto(22)
    assert(false, p is .x)
    assert(true, p is .foobar)
}

func objectTypeTest() {
    var obj = __makeObject( (name: "Vasily", age: 41) )
    var res1 = match obj {
        (x, y) => "x=\(x),y=\(y)",
        _ => "No."
    }
    assert("No.", res1)
    var res2 = match obj {
        (name: x, age: y) => "x=\(x),y=\(y)",
        _ => "No."
    }
    assert("x=Vasily,y=41", res2)
}

type Julia(x)
func callableTest1() {
    func Julia.call(y, z) {
        this::x + y * z 
    }
    var x = Julia(12)
    var res = x(44.5, 67.07)
    assert(2996.615, res)
}

func callableTest2() {
    func Integer.call() {
        this + this
    }
    var res = 14()
    Integer.__deleteMember("call")
    assert(28, res)
}

type TypesOption = Some(x) or None()
func testIs() {
    let some = TypesOption.Some(42)
    let none = TypesOption.None()
    var x
    x = 42 when some is TypesOption && none is TypesOption
    assert(42, x)
}

func testCustomFunc() {
    func TypesOption.extractValue() {
        if this is Some(x) {
            x
        }
    }
    let some = TypesOption.Some(42)
    assert(42, some.extractValue())
}

type TStringBuilder = {
    let buffer = []
}
func stringBuilderTest() {
    static func TStringBuilder.TStringBuilder() { }
    func TStringBuilder.append(val) {
        this.ini.buffer.add(val.toString())
    }
    func TStringBuilder.clear() {
        this.ini.buffer.clear()
    }
    func TStringBuilder.toString() {
        String.concat(values: this.ini.buffer)
    }
    func TStringBuilder.len() {
        var count = 0
        for x in this.ini.buffer {
            count += x.len()
        }
        return count
    }
    let sb = TStringBuilder()
    sb.append("my ")
    sb.append("rank ")
    sb.append("is ")
    sb.append(123)
    assert("my rank is 123", sb.toString())
    assert(14, sb.len())
    sb.clear()
    sb.append(true)
    sb.append(false)
    assert("truefalse", sb.toString())
}

type PAccessTest_Foo = {
    var x
    let pi = 3.14
}
static func PAccessTest_Foo.PAccessTest_Foo(px) {
    this.ini.x = px
}
func privateAccessTest1() {
    func PAccessTest_Foo.ok() {
        return this.ini.x
    }
    var f = PAccessTest_Foo(12)
    var res = f.ok()
    assert(12, res)
    PAccessTest_Foo.__deleteMember("ok")
}

func privateAccessTest2() {
    func PAccessTest_Foo.ok(f) {
        func inner() {
            return this.ini.pi
        }
        return inner()
    }
    var f = PAccessTest_Foo(12)
    var res = f.ok(f)
    assert(3.14, res)
    PAccessTest_Foo.__deleteMember("ok")
}

type PAccessTest_Bar(x)
func PAccessTest_Bar.violator(f) {
    return f.ini.x
}
func privateAccessTest3() {
    var b = PAccessTest_Bar(42)
    var f = PAccessTest_Foo(42)
    try {
        b.violator(f)
        assert(got: false)
    } catch {
        PrivateAccess() => assert(got: true)
    }
}

type RealNat = {
    var num
}
func testNat() {
    static func RealNat.RealNat(x) {
        throw Error.InvalidType(x.getType()) when x is not Integer
        this.ini.num = x
    }
    func RealNat + (other) {
        let res = match other {
            RealNat => this.ini.num + other.ini.num,
            Integer => this.ini.num + other,
            _ => throw Error.InvalidType(other.getType())
        }
        RealNat(res) when res >= 0 else res
    }
    func RealNat == (other) => this.ini.num == other.ini.num
    var (x, y) = (RealNat(12), RealNat(10))
    assert(RealNat(22), x + y)
    assert(RealNat(2), x + -10)
    assert(-2, x + -14)
}

type TestTA(Integer x, Float y)
func typeConstraints1() {
    let _ = TestTA(12, 12.12)
    try {
        let _ = TestTA(12.2, 12)
        assert(got: false)
    } catch {
        InvalidType() => assert(got: true)
    }
}

type TestMut(var x, y)
func typeMutables1() {
    let t = TestMut(12, 24)
    t::x = 120
    assert(120, t::x)
    try {
        t::y = 240
        assert(got: false)
    } catch {
        FieldReadOnly() => assert(got: true)
    }
}

type TestMut2(var Integer x, y)
func typeMutables2() {
    let t = TestMut2(12, 24)
    t::x = 1
    assert(1, t::x)
    try {
        t::x = "240"
        assert(got: false)
    } catch {
        InvalidType() => assert(got: true)
    }
}

type TestCustomCons = {
    var data
}
func customConsWithConstraints() {
    static func TestCustomCons.TestCustomCons(Integer x) {
        this.ini.data = x
    }
    func TestCustomCons.getItem(field) {
        match field {
            "x" => this.ini.data,
            _ => Error.IndexOutOfRange()
        }
    }
    func TestCustomCons.setItem(field, Integer value) {
        match field {
            "x" => this.ini.data = value,
            _ => Error.IndexOutOfRange()
        }
    }
    let t = TestCustomCons(12)
    t::x = 120
    try {
        t::x = "120"
        assert(got: false)
    } catch {
        InvalidType() => assert(got: true),
        e => print(e)
    }
}

type TestExpr = Literal(var Float value) or Unary(var op, var TestExpr left)
func testTypeVars() {
    let a = TestExpr.Literal(12.2)
    assert(12.2, a::value)
    a::value = 44.4
    assert(44.4, a::value)
    let b = TestExpr.Unary("-", a)
    assert("-", b::op)
    assert(44.4, b::left::value)
    b::op = "+"
    assert("+", b::op)
    b::left::value = 42.0
    assert(42.0, b::left::value)
}

type TinyInt = {
    var value
    func getInteger(val) {
        val is TinyInt ? value : val
    }
}
func testPrivateInsideInit() {
    static func TinyInt.TinyInt(Integer x) {
        this.ini.value = this.ini.getInteger(x)
    }
    func TinyInt toInteger() => this.ini.value
    func TinyInt + (other) {
        TinyInt(this.ini.value + this.ini.getInteger(other))
    }
    let res = TinyInt(2) + TinyInt(7)
    print(9, res.toInteger())
}

type Foo14 = {
    var status
}
func testProperties1() {
    static func Foo14.Foo14(value) => this.ini.status = value
    func get Foo14.status() => this.ini.status
    func set Foo14.status(value) => this.ini.status = value
    let f = Foo14(10)
    assert(10, f.status)
    f.status = 21
    assert(21, f.status)
    f.status += 11
    assert(32, f.status)
}

type Foo15 = {
    var box
}
func testProperties2() {
    static func Foo15.Foo15() { }
    func get Foo15.box() => this.ini.box
    func set Foo15.box(value) => this.ini.box = value
    let f = Foo15()
    assert(42, f.box ?? 42)
    f.box ??= 144
    assert(144, f.box)
    f.box ??= 288
    assert(144, f.box)
}