type SimpleFoo(x)
func simpleTest1() {
    var f = SimpleFoo(42)
    assert(42, f::x)
    assert(42, f[0])
    assert(1, f.len())
    match f {
        SimpleFoo(x) => assert(42, x)
    }
}

type EmptyFoo()
func simpleTest2() {
    var f = EmptyFoo()
    assert(0, f.len())
    match f {
        EmptyFoo() => assert(true, true)
    }
}

type Asset = Computer(model, memory, rank) | Phone(model, memory) | Modem(model)
func simpleTest3() {
    var a1 = Asset.Computer(model: "Lenovo", memory: 16, rank: 3)
    var a2 = Asset.Phone("1+", 8)
    var a3 = Asset.Modem("Zyxel")
    assert(3, a1.len())
    assert(2, a2.len())
    assert(1, a3.len())
    assert("Lenovo"+16+3, a1::model+a1::memory+a1::rank)
    assert("1+"+8, a2::model+a2::memory)
    assert("Zyxel", a3::model)
    assert("Asset.Computer(model: \"Lenovo\", memory: 16, rank: 3)", a1.toString())
    assert("Asset.Phone(model: \"1+\", memory: 8)", a2.toString())
    assert("Asset.Modem(model: \"Zyxel\")", a3.toString())
}

type Void()
func simpleTest4() {
    var v = Void()
    assert(0, v.len())
    assert("Void()", v.toString())
}

func matchTypeTest1() {
    var a1 = Asset.Computer(model: "Lenovo", memory: 16, rank: 3)
    var a2 = Asset.Phone("1+", 8)
    func mem(a) {
        match a {
            Asset.Computer(model, memory, rank) => memory,
            Asset.Phone(model, memory) => memory
        }
    }
    assert(16, mem(a1))
    assert(8, mem(a2))
}

type Bar = {
    var data
}
func extendTest1() {
    static func Bar.Bar(args...) {
        data = args
    }
    func Bar.len() {
        data.len()
    }
    func Bar.get(index) {
        data[index]
    }
    var obj = Bar.Bar(1, 2, 3)
    var res = match obj {
        (x, y) => x + y,
        (x, y, z) => x + y + z
    }
    assert(6, res)
}

func extendTest2() {
    static func Bar.Bar(args...) {
        data = args
    }
    func Bar.len() {
        data.len()
    }
    func Bar.get(index) {
        data[index]
    }
    var obj = Bar(10, 20, 30)
    var res = match obj {
        (x, y) => x + y,
        (x, y, z) => x + y + z
    }
    assert(60, res)
}

type Maybe = None() | Some(x)
func autoConsTest1() {
    var x = Maybe.Some(42)
    var y = Maybe.None()
    func doMatch(x) {
        match x {
            Some(x) => x,
            None() => "None"
        }
    }
    assert(42, doMatch(x))
    assert("None", doMatch(y))
}

type Foo1 = Bar(x, y, z)
func autoConsTest2() {
    var f = Foo1.Bar(12, "foobar", 3)
    var res = match f {
        Bar(x: xx, y: yy, z: zz) => "\(xx),\(yy)",
        Bar(x, y, z) => x + z,
        Bar() => "not quite"
    }
    assert("12,foobar", res)
}

type Point(x, y)
func autoConsTest3() {
    var p = Point(12, 43)
    assert(12, p[0])
    assert(43, p[1])
    assert(12, p::x)
    assert(43, p::y)
}

type Nat = { var val }
func typeTest1() {
    func fetch(x) {
        if x < 0 {
            x
        } else {
            Nat(x)
        }
    }
    static func Nat.Nat(x) {
        if x < 0 {
            throw "Negative number."
        }
        val = x
    }
    func Nat.toInteger() {
        val
    }
    func Nat + (other) {
        fetch(val + other.toInteger())
    }
    func Nat - (other) {
        fetch(val - other.toInteger())
    }
    func Nat * (other) {
        fetch(val * other.toInteger())
    }
    func Nat / (other) {
        fetch(val / other.toInteger())
    }
    func Nat == (other) {
        val == other.toInteger()
    }
    var n = Nat(12)
    assert(Nat(24), n + Nat(12))
    assert(Nat(2), n - Nat(10))
    assert(-8, n - Nat(20))
    assert(Nat(24), n * Nat(2))
    assert(Nat(6), n / Nat(2))
    assert(Nat(6), n / Nat(2))
}

type Either = Left(x) | Right(x)
func typeTest2() {
    var x = Either.Left(12)
    var y = Either.Right(24)
    func doMatch(obj) {
        match obj {
            Left(x) => x,
            Right(y) => y
        }
    }
    assert(12, doMatch(x))
    assert(24, doMatch(y))
}

type Cell = { var val }
func typeTest3() {
    func ref(value) {
        Cell(value)
    }
    static func Cell.Cell(value) {
        val = value
    }
    func Cell.setValue(value) {
        val = value
    }
    func Cell.getValue() {
        val
    }
    func Cell.get(index) {
        if index != 0 {
            throw "Index out of range."
        }
        val
    }
    func Cell.len() {
        1
    }
    var c = ref(12)
    assert(12, c.getValue())
    assert(12, match c {
        Cell(x) => x
    })
    c.setValue(24)
    assert(24, c.getValue())
    assert(24, match c {
        Cell(x) => x
    })
}

type List = Cons(x, next) | Empty()
func typeTest4() {
    func List.toString() {
        func walk(xs) {
            match xs {
                Cons(x, Empty()) => x.toString(),
                Cons(x, next) => x.toString() + "," + walk(next),
                Empty() => ""
            }
        }
        return "[" + walk(this) + "]"
    }
    var xs = List.Cons(1, List.Cons(2, List.Cons(3, List.Cons(4, List.Empty()))))
    assert("[1,2,3,4]", xs.toString())
}

func typeTest5() {
    static func List.generate(values...) {
        var xs = List.Empty()
        for x in values {
            xs = List.Cons(x, xs)
        }
        xs
    }
    var xs = List.generate(1,2,3,4)
    assert("[4,3,2,1]", xs.toString())
}

type Zoo1()
func typeTest6() {
    var x = Zoo1()
    func Zoo1.toString() {
        match this {
            Zoo1() => "nil"
        }
    }
    assert("nil", x.toString())
}

type Zeta()
func simpleTypeTest1() {
    var z = Zeta()
    var res = z.len()
    assert(0, res)
}

type Beta(x)
func simpleTypeTest2() {
    var b = Beta(12)
    assert(1, b.len())
    assert(12, b[0])
    assert(12, b["x"])
    assert(12, b::x)
}

type Record = { var fields }
func accessorTest() {
    static func Record.Record(values...) {
        fields = values
    }
    func Record.get(index) {
        if index == "name" {
            return fields::name
        } else if index == "age" {
            return fields::age
        }
    }
    var p = Record(values: (name: "John", age: 33))
    assert("John", p["name"])
    assert(33, p::age)
}

type UserInfo(name,age)
func setterTest1() {
    var u = UserInfo("Jon", 17)
    assert(17, u::age)
    u::age = 39
    assert(39, u::age)
}

type Point1 = { var px; var py }
func setterTest2() { //Bug #205
    static func Point1.Point1(x, y) {
        px = x
        py = y
    }
    func Point1.get(index) {
        if index == 0 || index == "x" {
            x
        } else if index == 1 || index == "y" {
            y
        } else {
            throw Error.IndexOutOfRange()
        }
    }
    func Point1.set(index, value) {
        if index == 0 || index == "x" {
            x = value
        } else if index == 1 || index == "y" {
            y = value
        } else {
            throw Error.IndexOutOfRange()
        }
    }
    var p = Point1(12, 34)
    p::x += p::x
    p::y += p::y
    assert(24, p::x)
    assert(68, p::y)
}

func complexMemberCheckTest1() {
    var t = (x: 2)
    assert(false, t.x?)
}

func complexMemberCheckTest2() {
    func Integer.foobar() {}
    assert(true, 12.foobar?)
    Integer.__deleteMember("foobar")
}

type Leto(x)
func complexMemberCheckTest3() {
    func Leto.foobar() {}
    var p = Leto(22)
    assert(false, p.x?)
    assert(true, p.foobar?)
}

func objectTypeTest() {
    var obj = makeObject( (name: "Vasily", age: 41) )
    var res1 = match obj {
        (x, y) => "x=\(x),y=\(y)",
        _ => "No."
    }
    assert("No.", res1)
    var res2 = match obj {
        (name: x, age: y) => "x=\(x),y=\(y)",
        _ => "No."
    }
    assert("x=Vasily,y=41", res2)
}

type Julia(x)
func callableTest1() {
    func Julia.call(y, z) {
        this::x + y * z 
    }
    var x = Julia(12)
    var res = x(44.5, 67.07)
    assert(2996.615, res)
}

func callableTest2() {
    func Integer.call() {
        this + this
    }
    var res = 14()
    Integer.__deleteMember("call")
    assert(28, res)
}