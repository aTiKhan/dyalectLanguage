func catchTest1() {
    var x
    try {
        x = 2
    } catch e {
        x = 22
    }
    //catch should be ignored
    assert(2, x)
}

func catchTest2() {
    var res =
        try {
            2 / 0
        } catch e {
            42
        }
    assert(42, res)
}

func catchTest3() {
    var res =
        try {
            try {
                2 / 0
            }
            catch e {
                142
            }
        } catch e {
            42
        }
    assert(142, res)
}

func catchTest4() {
    var res =
        try {
            throw "Error!"
        } catch e {
            e
        }
    assert("Error!", res)
}

func catchTest5() {
    var res =
        try {
            try {
                throw "Oops!"
            }
            catch e {
                throw e
            }
        } catch e {
            "caught:" + e
        }
    assert("caught:Oops!", res)
}

func catchTest6() {
    var res =
        try {
            try {
                throw 42
            }
            catch {
                0 => 0,
                1 => 1,
                2 => 2
            }
        } catch e {
            e
        }
    assert(42, res)
}

func catchTest7() {
    func errorMaker() {
        throw nameof(errorMaker)
    }
    func caller2() {
        errorMaker()
    }
    func caller1() {
        caller2()
    }
    func caller0(){
        try {
            caller1()
        } catch e {
            e
        }
    }
    var res = caller0()
    assert("errorMaker", res)
}

func catchTest8() {
    func foo(x, y, z) { (x,y,z) }
    var e = try {
            foo(1, 2, 3, 4)
        } catch e {
            "foo"
        }
    assert("foo", e)
}

func catchTest9() {
    func check(x) {
        if x % 2 == 0 {
            throw x
        }
    }
    func yielder(arr) { 
        for x in arr {
            try {
                check(x)
                yield x
            } catch _ {
                //Do nothing
            }
        }
    }
    var arr = [1..20]
    assert([1,3,5,7,9,11,13,15,17,19], yielder(arr).toArray())
}

func catchTest10() {
    var res =
        try {
            4 / 2
        } catch e {
            42
        }
    assert(2, res)
}

func catchMatchTest1() {
    func foo() { throw ("err", 22) }
    var e = try {
            foo()
        } catch {
            ("err", 22, 1) => false,
            ("err", 23) => false,
            ("err", 22) => true
        }
    assert(true, e)
}

func catchMatchTest2() {
    func foo() { throw ("err", 231) }
    var e
    e = try {
        e = try {
                foo()
            } catch {
                ("err", 22, 1) => false,
                ("err", 23) => false,
                ("err", 22) => true
            }
    } catch e {
        142
    }
    assert(142, e)
}

func externalCatchTest1() {
    func sorter(x, y) {
        if x > 10 {
            throw "No!"
        }
        x - y
    }
    var arr = [1..11]
    var ex
    try {
        arr.sort(sorter)
    } catch e {
        ex = e
    }
    assert("No!", ex)
}

func externalCatchTest2() {
    func defaultElement() {
        var n = 0
        while true {
            n += 1
            yield n
            if n > 10 {
                throw n
            }
        }
    }
    var arr = Array.empty(10, defaultElement())
    assert([1,2,3,4,5,6,7,8,9,10], arr)
    try {
        Array.empty(11, defaultElement())
    } catch e {
        assert(11, e)
    }
}

func externalCatchTest3() {
    func seq() {
        yield 1
        throw 2
    }
    try {
        var arr = []
        arr.addRange(seq())
    } catch e {
        assert(2, e)
    }
}

func iteratorExceptionTest1() {
    func test() {
        var xs = [1..2]
        var seq = xs.iter()
        var x = seq()
        xs.removeAt(1)
        x = seq()
        x + x
    }
    var b = try {
        test()
    } catch e {
        e.contains("iterator")
    }
    assert(true, b)
}

func iteratorExceptionTest2() {
    func test() {
        var xs = [1..2]
        var seq = xs.iter()
        var x = seq()
        xs.addRange({3, 4, 5})
        x = seq()
        x + x
    }
    var b = try {
        test()
    } catch e {
        e.contains("iterator")
    }
    assert(true, b)
}

type U0()
func customTypeException() { //Bug #248
    func baz() {
        throw U0()
    }
    func bar() {
        baz()
    }
    func foo() {
        try {
            bar()
        } catch {
            U0() => "U0 caught."
        }
    }
    assert("U0 caught.", foo())
}

func chainCatch1() {
    func zoo() {
        throw 1024
    }
    func bar() {
        zoo()
    }
    func foo() {
        try {
            bar()
        } catch e {
            if e == 1024 {
                throw
            }
        }
    }
    var x
    try {
        foo()
    } catch e {
        x = e
    }
    assert(1024, x)
}

func rightCatch() {
    func checkit() {
        try {
            return
        } catch _ {
            throw "Invalid catch."
        }
    }
    func run() {
        checkit()
        throw 12
    }
    var x
    try {
        run()
    } catch e {
        x = e
    }
    assert(12, x)
}