///////////////////////////////////////////////
//Basic tests
///////////////////////////////////////////////
func mathTest1() {
    12 * 3 - 4 + 6
}

func mathTest2() { //1.5919117647058822
    22 % 4 - 3.33 / 8.16
}

func mathTest3() { //6.6129032258064528
    12.3 / (0.64 + 1.22)
}

func mathTest4() { //11.256
    23.456 % 12.2
}

func mathTest5() { //0.0099999999999997868
   12.4 - 0.39 - 12.0
}

func mathTest6() { //783.59999999999991
   12.0 * 65.3
}

func mathTest7() {
    188.123 - 12.0004 * 5.667 - (12.2 / 0.134)
}

func mathTest8() {
    -12 - 14.6
}

func mathTest9() {
    199 % 44.12 * 2
}

func mathTest10() {
    +16 - -22
}

func logicalTest1() {
    const x = 144
    x / 12 > 100 || x / 6 == 24
}

func logicalTest2() {
    const x = 144
    x / 12 > 100 && x / 6 != 24
}

func comparisonTest1() {
    12 > 11 && 12 <= 12 && 12 != 12.01 && 12 >= 12
}

func comparisonTest2() {
    "string1" != "string2" && "string1" == "string1"
}

func comparisonTest3() {
    const x = 12
    const y = 9
    !(x >= 12 && y <= 10 || x < -12)
}

func toStringTest1() {
    (1,2,3).toString()
}

func toStringTest2() {
    [12.2, "string", true].toString()
}

func toStringTest3() {
    (x:42,y:"foo").toString()
}

func baseTest1() {
    const x = 2
    func inner() {
        const x = 4
        base.x
    }
    inner()
}

func baseTest2() {
    const x = 2
    func inner() {
        const x = 4
        if x == 4 {
            var x = 8
            base.x
        }
    }
    inner()
}

func literalTest1() {
    var tup = (
        x: (x: 2, y: (y: 3)),
        22
    )
    tup[1] + tup["x"]["x"] - tup["x"]["y"]["y"]
}

func literalTest2() {
    var arr = [[1,2], [3,4,(5,6,a:[7,8])]]
    arr[0][0] + arr[1][2]["a"][1]
}

func literalTest3() {
    "some\tstring with\"inner quotes\" and such (and a buck \u0024)"
}

func literalTest4() {
    'some string with ''quotes''!'
}

func literalTest5() {
    1.234e-17
}

func literalTest6() {
    0x40 + 0x20
}

func arrayTest1() {
    var arr = []
    arr.add(1)
    arr.add(2)
    arr.addRange([1,2,3])
    arr.removeAt(1)
    arr.remove(3)
    arr.toString()
}

func arrayTest2() {
    var arr = [1,2,3,4,5]
    arr.clear()
    arr.add(1)
    arr.add(2)
    arr.insert(0, 11)
    arr.insert(0, 11)
    arr.remove(11)
    arr.toString()
}

///////////////////////////////////////////////
//Complex tests
///////////////////////////////////////////////
func whileTest1() {
    var n = 100
    var x = while n > 0 {
        if n == 33 {
            break n
        }
        n = n - 1
    }
    return x
}

func whileTest2() {
    var n = 100
    var x = while n > 0 {
        if n == 33 {
            break
        }
        n = n - 1
    }
    if !x {
        x = 42
    }
    return x
}

func fizzbuzzTest() {
    var n = 1
    var acc = ""
    while n < 11 {
        if n % 15 == 0 {
            acc = acc + "fizzbuzz"
        } else if n % 3 == 0 {
            acc = acc + "fizz"
        } else if n % 5 == 0 {
            acc = acc + "buzz"
        } else {
            acc = acc + n
        }
        n = n + 1
    }
    acc
}

func recursionTest() {
    var acc = 0
    func iter(n) {
        if n == 0 {
            0
        } else {
            acc = acc + 1;
            iter(n - 1)
        }
    }
    iter(10)
    acc
}

func factTest() {
    func fact(n) {
        if n == 1 {
            1
        } else {
            n * fact(n - 1)
        }
    }
    fact(20)
}

func powerTest() {
    func pow(basis, exponent) {
        if exponent == 1 {
            basis
        } else if exponent == 0 {
            1
        } else {
            basis * pow(basis, exponent - 1)
        }
    }
    pow(10, 3)
}

func binaryConversionTest() {
    var acc = ""
    func bin(num) {
        if num != 0 {
            const bin = (num % 2) + 10 * bin(num / 2)
            acc += bin
            0
        } else {
            0
        } 
    }
    bin(157)
    acc
}

func fibTest1() {
    func fib(n) {
        if n == 0 || n == 1 {
            return n
        }
        fib(n - 1) + fib(n - 2)
    }
    fib(11)
}

func fibTest2() {
    func fib(accum,a,b,nums) {
        if nums > 0 {
            accum = 
                if accum {
                    accum + " " + a
                } else {
                    a.toString()
                }
            fib(accum, b, a + b, nums - 1)
        } else {
            accum
        }
    }
    fib(nil, 0, 1, 11)
}

func phoneParserTest() {
    func digit(c) {
           c == "0"
        || c == "1"
        || c == "2"
        || c == "3"
        || c == "4"
        || c == "5"
        || c == "6"
        || c == "7"
        || c == "8"
        || c == "9"
    }
    func parse(str) {
        var max = str.len()
        var i = 0;
        var res = "";
        while i < max {
            const c = str[i]
            if c == "+" {
                i += 2
                continue
            }
            if digit(c) {
                res += c
            }
            i += 1
        }
        res
    }
    parse("+7 (964) 506-11-12")
}

func iteratorTest1() {
    func foo(token) {
        yield 1
        yield 3
        yield 44.2
        if token {
            return
        }
        yield 68.6
    }
    var it = foo(false)
    it() + it() + it() + it()
}

func iteratorTest2() {
    var arr = [1,2,3,4,5,6,7,8]
    func sum(arr) {
        var acc = 0
        for x in arr {
            acc += x
        }
        acc
    }
    sum(arr)
}

func iteratorTest3() {
    var arr = [1,2,3,4,5,6,7,8]
    func filter(arr, pred) {
        for x in arr {
            if pred(x) {
                yield x
            }
        }
    }
    func sum(arr) {
        var acc = 0
        for x in arr {
            acc += x
        }
        acc
    }
    var filtered = filter(arr, x => x % 2 == 0)
    sum(filtered)
}

func iteratorTest4() {
    func reverse(arr) {
        var newArr = []
        newArr.addRange(arr) //hack
        var len = newArr.len()
        var counter = 1
        for x in arr {
            newArr[len - counter] = x
            counter += 1
        }
        newArr
    }
    func makeString(arr) {
        var str = ""
        for x in arr {
            str += x
        }
        str
    }
    const arr = reverse("Hello")
    makeString(arr)
}

func iteratorTest5() {
    func filter(arr, pred) {
        for x in arr {
            if pred(x) {
                yield x
            }
        }
    }
    func toArray(seq) {
        var arr = []
        for x in seq {
            arr.add(x)
        }
        arr
    }
    const arr = toArray( filter("hello", x => x == "l") )
    arr[0] + arr[1]
}

func iteratorTest6() {
    var arr = [1,3,5,8]
    var x = 
        for x in arr {
            if x % 2 == 0 {
                break x
            }
        }
    x
}

func iteratorTest7() {
    func Integer.iter() {
        var i = 0
        if this < 0 {
            while i > this {
                yield i
                i -= 1
            }
        } else {
            while i < this {
                yield i
                i += 1
            }
        }
    }

    5.iter().toArray().toString()
}

func calcETest() { //2.71828182845905
    const epsilon = 1.0e-15
    func abs(n) {
        if n < 0 {
            -n
        } else {
            n
        }
    }
    var fact = 1
    var e = 2.0
    var e0
    var n = 2
    while true {
        e0 = e
        fact *= n
        n += 1
        e += 1.0 / fact

        if abs(e - e0) < epsilon {
            break
        }
    }
    return e
}

func dammTest() {
    const table = [
        [0, 3, 1, 7, 5, 9, 8, 6, 4, 2],
        [7, 0, 9, 2, 1, 5, 4, 8, 6, 3],
        [4, 2, 0, 6, 8, 7, 1, 3, 5, 9],
        [1, 7, 5, 0, 9, 8, 3, 4, 2, 6],
        [6, 1, 2, 3, 0, 4, 5, 9, 7, 8],
        [3, 6, 7, 4, 2, 0, 9, 5, 8, 1],
        [5, 8, 6, 9, 7, 2, 0, 1, 3, 4],
        [8, 9, 4, 5, 3, 6, 2, 0, 1, 7],
        [9, 4, 3, 8, 6, 1, 7, 2, 0, 5],
        [2, 5, 8, 1, 4, 3, 6, 7, 9, 0]
    ] 
    func damm(num) {
        var interim = 0
        for c in num.toString() {
            interim = table[interim][convertToNumber(c)];
        }
        interim == 0
    }
    var numbers = [5724, 5727, 112946, 112949]
    var results = []
    for num in numbers {
        var isValid = damm(num)
        if isValid {
            results.add("yes")
        } else {
            results.add("no")
        }
    }
    var str = ""
    for e in results {
        str += e
    }
    str
}

func eulerTest() {
    const T0 = 100.0
    const TR = 20.0
    const k = 0.07
    const n = 100
    func newtonCooling(t) {
        -k * (t - TR)
    } 
    func euler(fn, y, n, h) {
        var values = []
        var x = 0
        while x <= n {
            values.add(x + "=" + y)
            y += h * fn(y)
            x += h
        }
        values
    }
    const arr = euler(newtonCooling, T0, n, 10.0)
    var str = ""
    for e in arr {
        str += e + ";"
    }
    str
}

func gcdTest() {
    func gcd(a, b) {
        if a != 0 && b != 0 {
            if a > b {
                a %= b
            } else {
                b %= a
            }
        } else if a {
            a
        } else  {
            b
        }
    }
    gcd(24, 36)
}

func lcmTest() {
    func gcd(a, b) {
        if a != 0 && b != 0 {
            if a > b {
                a %= b
            } else {
                b %= a
            }
        } else if a {
            a
        } else  {
            b
        }
    }
    func lcm(a, b) {
        a * b / gcd(a, b)
    }
    lcm(10, 135)
}

func ackTest() {
    func ack(m, n) {
        if m > 0 {
            if n > 0 {
                ack(m - 1, ack(m, n - 1))
            } else if n == 0 {
                ack(m - 1, 1)
            } else {
                "OutOfRange"
            }
        }
        else if m == 0 && n > 0 {
            n + 1
        } else {
            "OutOfRange"
        }
    }
    ack(3, 4)
}

func foldTest() {
    func fold(xs, fn) {
        var accum = 0
        for x in xs {
            accum = fn(accum, x)
        }
        accum
    }
    fold([1,2,3,4,5,6], (acc,x) => acc + x)
}

func factorsTest() {
    func factors(num) {
        const arr = []
        var x = 1
        while x <= num {
            if num % x == 0 {
                arr.add(x)
            }
            x += 1
        }
        arr
    }
    factors(27).toString()
}

func antiprimeTest() {
    func countDivisors(n) {
        if n < 2 {
            return 1
        }
        var count = 2
        var i = 2
        while i <= n / 2 {
            if n % i == 0 {
                count += 1
            }
            i += 1
        }
        count
    }
    countDivisors(20)
}