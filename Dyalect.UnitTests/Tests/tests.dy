///////////////////////////////////////////////
//Basic tests
///////////////////////////////////////////////
func mathTest1() {
    var x = 12 * 3 - 4 + 6
    assert(38, x)
}

func mathTest2() { //1.5919117647058822
    var x = 22 % 4 - 3.33 / 8.16
    assert(1.5919117647058822, x)
}

func mathTest3() { //6.6129032258064528
    var x = 12.3 / (0.64 + 1.22)
    assert(6.6129032258064528, x)
}

func mathTest4() { //11.256
    var x = 23.456 % 12.2
    assert(11.256, x)
}

func mathTest5() { //0.0099999999999997868
   var x = 12.4 - 0.39 - 12.0
   assert(0.0099999999999997868, x)
}

func mathTest6() { //783.59999999999991
   var x = 12.0 * 65.3
   assert(783.59999999999991, x)
}

func mathTest7() {
    var x = 188.123 - 12.0004 * 5.667 - (12.2 / 0.134)
    assert(29.071957080597016, x)
}

func mathTest8() {
    var x = -12 - 14.6
    assert(-26.6, x)
}

func mathTest9() {
    var x = 199 % 44.12 * 2
    assert(45.04000000000002, x)
}

func mathTest10() {
    var x = +16 - -22
    assert(38, x)
}

func logicalTest1() {
    const x = 144
    var res = x / 12 > 100 || x / 6 == 24
    assert(true, res)
}

func logicalTest2() {
    const x = 144
    var res = x / 12 > 100 && x / 6 != 24
    assert(false, res)
}

func comparisonTest1() {
    var res = 12 > 11 && 12 <= 12 && 12 != 12.01 && 12 >= 12
    assert(true, res)
}

func comparisonTest2() {
    assert(true, "string1" != "string2" && "string1" == "string1")
}

func comparisonTest3() {
    const x = 12
    const y = 9
    assert(false, !(x >= 12 && y <= 10 || x < -12))
}

func comparisonTest4() {
    assert(true, 12.0 == 12)
    assert(true, 12 == 12.0)
}

func comparisonTest5() {
    assert(true, 12.2 > 12)
    assert(true, 12 < 12.2)
}

func toStringTest1() {
    var res = (1,2,3).toString()
    assert("(1, 2, 3)", res)
}

func toStringTest2() {
    var res = [12.2, "string", true].toString()
    assert("[12.2, \"string\", true]", res)
}

func toStringTest3() {
    var res = (x:42,y:"foo").toString()
    assert("(x: 42, y: \"foo\")", res)
}

func baseTest1() {
    const x = 2
    func inner() {
        const x = 4
        base.x
    }
    assert(2, inner())
}

func baseTest2() {
    const x = 2
    func inner() {
        const x = 4
        if x == 4 {
            var x = 8
            base.x
        }
    }
    assert(2, inner())
}

func literalTest1() {
    var tup = (
        x: (x: 2, y: (y: 3)),
        22
    )
    var res = tup[1] + tup["x"]["x"] - tup["x"]["y"]["y"]
    assert(21, res)
}

func literalTest2() {
    var arr = [[1,2], [3,4,(5,6,a:[7,8])]]
    var res = arr[0][0] + arr[1][2]["a"][1]
    assert(9, res)
}

func literalTest3() {
    var fst = "some\tstring with\"inner quotes\" and such (and a buck \u0024)"
    var snd = "some\u0009string with\u0022inner quotes\u0022 and such (and a buck $)"
    assert(fst, snd)
}

func literalTest4() {
    var res = '\'' + 'F' + '\''
    assert("'F'", res)
}

func literalTest5() {
    var res = 1.234e-17
    assert(0.00000000000000001234, res)
}

func literalTest6() {
    var res = 0x40 + 0x20
    assert(96, res)
}

func arrayTest1() {
    var arr = []
    arr.add(1)
    arr.add(2)
    arr.addRange([1,2,3])
    arr.removeAt(1)
    arr.remove(3)
    assert([1, 1, 2], arr)
}

func arrayTest2() {
    var arr = [1,2,3,4,5]
    arr.clear()
    arr.add(1)
    arr.add(2)
    arr.insert(0, 11)
    arr.insert(0, 11)
    arr.remove(11)
    assert([11, 1, 2], arr)
}

func arrayTest3() {
    var arr = [1,2,3,4,5,6]
    var newArr = arr.slice(2,5)
    assert([3, 4, 5], newArr)
}

func arrayTest4() {
    var arr = [4,6,1,3,2,5]
    arr.sortBy((x,y) => x - y)
    assert([1, 2, 3, 4, 5, 6], arr)
}

func arrayTest5() {
    var arr = [1, 2, 3, 1, 4, 5, 1]
    assert(0, arr.indexOf(1))
    assert(6, arr.lastIndexOf(1))
}

func arrayTest6() {
    var arr = [1,2,3,4,5,6]
    var res = arr.indices().toArray()
    assert([0, 1, 2, 3, 4, 5], res)
}

func arrayTest7() {
    var arr = [1,nil,2,nil,3,nil,nil,4,5,6]
    arr.compact()
    assert([1, 2, 3, 4, 5, 6], arr)
}

func arrayTest8() {
    var arr = [1,2,3,4]
    assert(4, arr.len())
}

func arrayTest9() {
    var arr = [7,4,6,1,3,2,5]
    arr.sort()
    assert([1, 2, 3, 4, 5, 6, 7], arr)
}

func tupleTest1() {
    var t = (1,2,3)
    assert(3, t.len())
}

func tupleTest2() {
    var t = (x: 1, y: 2, z: 3)
    assert(6, t.x + t.y + t.z)
}

func tupleTest3() {
    var t = (x: 1, y: 2, z: 3)
    var res = t.keys().toArray()
    assert(["x", "y", "z"], res)
}

func tupleTest4() {
    var t = (1,2,3,4,5,6)
    var res = t.indices().toArray()
    assert([0, 1, 2, 3, 4, 5], res)
}

func tupleTest5() {
    var t = (42, 4.56)
    var res = t.fst() - t.snd()
    assert(37.44, res)
}

func stringTest1() {
    var str = "Hello, world!"
    var res = str.len()
    assert(13, res)
}

func stringTest2() {
    var str = "Hello, world!"
    assert(4, str.indexOf("o"))
    assert(8, str.lastIndexOf("o"))
}

func stringTest3() {
    assert(false, "1" > "2")
    assert(true, "1" < "2")
}

func stringTest4() {
    var str = "Name=John;Surname=Doe;Age=21;Gender=Male"
    var arr = str.split('=', ';')
    assert(["Name","John","Surname","Doe","Age","21","Gender","Male"], arr)
}

func stringTest5() {
    var str = "foo"
    var res = str[0] + str[2]
    assert("fo", res)
}

func stringTest6() {
    var str1 = "FOO"
    var str2 = "bar"
    var res = str1.lower() + str2.upper()
    assert("fooBAR", res)
}

func stringTest7() {
    var res = "fooBar".startsWith("foo")
    assert(true, res)
}

func stringTest8() {
    var res = "fooBar".endsWith("Bar")
    assert(true, res)
}

func stringTest9() {
    var res = "abcdef".sub(2, 4) + "qwerty".sub(4)
    assert("cdefty", res)
}

func stringTest10() {
    var res = "camelCase".capitalize()
    assert("CamelCase", res)
}

func stringTest11() {
    var res = String.concat("one", "two", "three", 44)
    assert("onetwothree44", res)
}

func stringTest12() {
    var res = "fooBar".contains("oBa")
    assert(true, res)
}

func stringTest13() {
    var res = " ss".trim() + "--dd--".trimStart('-') + "!ee!".trimEnd('!')
    assert("ssdd--!ee", res)
}

func memberCheckTest() {
    assert(true, "string".len?)
    assert(false, 42.fooBar?)
}

///////////////////////////////////////////////
//Complex tests
///////////////////////////////////////////////
func whileTest1() {
    var n = 100
    var x = while n > 0 {
        if n == 33 {
            break n
        }
        n = n - 1
    }
    assert(33, x)
}

func whileTest2() {
    var n = 100
    var x = while n > 0 {
        if n == 33 {
            break
        }
        n = n - 1
    }
    if !x {
        x = 42
    }
    assert(42, x)
}

func fizzbuzzTest() {
    var n = 1
    var acc = ""
    while n < 11 {
        if n % 15 == 0 {
            acc = acc + "fizzbuzz"
        } else if n % 3 == 0 {
            acc = acc + "fizz"
        } else if n % 5 == 0 {
            acc = acc + "buzz"
        } else {
            acc = acc + n
        }
        n = n + 1
    }
    assert(acc, "12fizz4buzzfizz78fizzbuzz")
}

func recursionTest() {
    var acc = 0
    func iter(n) {
        if n == 0 {
            0
        } else {
            acc = acc + 1;
            iter(n - 1)
        }
    }
    iter(10)
    assert(acc, 10)
}

func factTest() {
    func fact(n) {
        if n == 1 {
            1
        } else {
            n * fact(n - 1)
        }
    }
    var res = fact(20)
    assert(2432902008176640000, res)
}

func powerTest() {
    func pow(basis, exponent) {
        if exponent == 1 {
            basis
        } else if exponent == 0 {
            1
        } else {
            basis * pow(basis, exponent - 1)
        }
    }
    var x = pow(10, 3)
    assert(x, 1000)
}

func binaryConversionTest() {
    var acc = ""
    func bin(num) {
        if num != 0 {
            const bin = (num % 2) + 10 * bin(num / 2)
            acc += bin
            0
        } else {
            0
        } 
    }
    bin(157)
    assert("10011101", acc)
}

func fibTest1() {
    func fib(n) {
        if n == 0 || n == 1 {
            return n
        }
        fib(n - 1) + fib(n - 2)
    }
    var x = fib(11)
    assert(x, 89)
}

func fibTest2() {
    func fib(accum,a,b,nums) {
        if nums > 0 {
            accum = 
                if accum {
                    accum + " " + a
                } else {
                    a.toString()
                }
            fib(accum, b, a + b, nums - 1)
        } else {
            accum
        }
    }
    var res = fib(nil, 0, 1, 11)
    assert("0 1 1 2 3 5 8 13 21 34 55", res)
}

func phoneParserTest() {
    func digit(c) {
           c == "0"
        || c == "1"
        || c == "2"
        || c == "3"
        || c == "4"
        || c == "5"
        || c == "6"
        || c == "7"
        || c == "8"
        || c == "9"
    }
    func parse(str) {
        var max = str.len()
        var i = 0;
        var res = "";
        while i < max {
            const c = str[i]
            if c == "+" {
                i += 2
                continue
            }
            if digit(c) {
                res += c
            }
            i += 1
        }
        res
    }
    var res = parse("+7 (964) 506-11-12")
    assert("9645061112", res)
}

func iteratorTest1() {
    func foo(token) {
        yield 1
        yield 3
        yield 44.2
        if token {
            return
        }
        yield 68.6
    }
    var it = foo(false)
    var res = it() + it() + it() + it()
    assert(116.8, res)
}

func iteratorTest2() {
    var arr = [1,2,3,4,5,6,7,8]
    func sum(arr) {
        var acc = 0
        for x in arr {
            acc += x
        }
        acc
    }
    var res = sum(arr)
    assert(36, res)
}

func iteratorTest3() {
    var arr = [1,2,3,4,5,6,7,8]
    func filter(arr, pred) {
        for x in arr {
            if pred(x) {
                yield x
            }
        }
    }
    func sum(arr) {
        var acc = 0
        for x in arr {
            acc += x
        }
        acc
    }
    var filtered = filter(arr, x => x % 2 == 0)
    var res = sum(filtered)
    assert(20, res)
}

func iteratorTest4() {
    func reverse(arr) {
        var newArr = []
        newArr.addRange(arr) //hack
        var len = newArr.len()
        var counter = 1
        for x in arr {
            newArr[len - counter] = x
            counter += 1
        }
        newArr
    }
    func makeString(arr) {
        var str = ""
        for x in arr {
            str += x
        }
        str
    }
    const arr = reverse("Hello")
    var res = makeString(arr)
    assert("olleH", res)
}

func iteratorTest5() {
    func filter(arr, pred) {
        for x in arr {
            if pred(x) {
                yield x
            }
        }
    }
    func toArray(seq) {
        var arr = []
        for x in seq {
            arr.add(x)
        }
        arr
    }
    const arr = toArray( filter("hello", x => x == 'l') )
    var res = arr[0] + arr[1]
    assert("ll", res)
}

func iteratorTest6() {
    var arr = [1,3,5,8]
    var x = 
        for x in arr {
            if x % 2 == 0 {
                break x
            }
        }
    assert(8, x)
}

func iteratorTest7() {
    func Integer.iter() {
        var i = 0
        if this < 0 {
            while i > this {
                yield i
                i -= 1
            }
        } else {
            while i < this {
                yield i
                i += 1
            }
        }
    }

    var res = 5.iter().toArray()
    assert([0,1,2,3,4], res)
}

func calcETest() { //2.71828182845905
    const epsilon = 1.0e-15
    func abs(n) {
        if n < 0 {
            -n
        } else {
            n
        }
    }
    var fact = 1
    var e = 2.0
    var e0
    var n = 2
    while true {
        e0 = e
        fact *= n
        n += 1
        e += 1.0 / fact

        if abs(e - e0) < epsilon {
            break
        }
    }
    assert(2.7182818284590455, e)
}

func dammTest() {
    const table = [
        [0, 3, 1, 7, 5, 9, 8, 6, 4, 2],
        [7, 0, 9, 2, 1, 5, 4, 8, 6, 3],
        [4, 2, 0, 6, 8, 7, 1, 3, 5, 9],
        [1, 7, 5, 0, 9, 8, 3, 4, 2, 6],
        [6, 1, 2, 3, 0, 4, 5, 9, 7, 8],
        [3, 6, 7, 4, 2, 0, 9, 5, 8, 1],
        [5, 8, 6, 9, 7, 2, 0, 1, 3, 4],
        [8, 9, 4, 5, 3, 6, 2, 0, 1, 7],
        [9, 4, 3, 8, 6, 1, 7, 2, 0, 5],
        [2, 5, 8, 1, 4, 3, 6, 7, 9, 0]
    ] 
    func damm(num) {
        var interim = 0
        for c in num.toString() {
            interim = table[interim][convertToNumber(c)];
        }
        interim == 0
    }
    var numbers = [5724, 5727, 112946, 112949]
    var results = []
    for num in numbers {
        var isValid = damm(num)
        if isValid {
            results.add("yes")
        } else {
            results.add("no")
        }
    }
    var str = ""
    for e in results {
        str += e
    }
    assert("yesnoyesno", str)
}

func eulerTest() {
    const T0 = 100.0
    const TR = 20.0
    const k = 0.07
    const n = 100
    func newtonCooling(t) {
        -k * (t - TR)
    } 
    func euler(fn, y, n, h) {
        var values = []
        var x = 0
        while x <= n {
            values.add(x + "=" + y)
            y += h * fn(y)
            x += h
        }
        values
    }
    const arr = euler(newtonCooling, T0, n, 10.0)
    var str = ""
    for e in arr {
        str += e + ";"
    }
    assert("0=100;10=44;20=27.2;30=22.16;40=20.648;50=20.1944;60=20.05832;70=20.017496;80=20.0052488;90=20.00157464;100=20.000472392;", str)
}

func gcdTest() {
    func gcd(a, b) {
        if a != 0 && b != 0 {
            if a > b {
                a %= b
            } else {
                b %= a
            }
        } else if a {
            a
        } else  {
            b
        }
    }
    var x = gcd(24, 36)
    assert(12, x)
}

func lcmTest() {
    func gcd(a, b) {
        if a != 0 && b != 0 {
            if a > b {
                a %= b
            } else {
                b %= a
            }
        } else if a {
            a
        } else  {
            b
        }
    }
    func lcm(a, b) {
        a * b / gcd(a, b)
    }
    var x = lcm(10, 135)
    assert(270, x)
}

func ackTest() {
    func ack(m, n) {
        if m > 0 {
            if n > 0 {
                ack(m - 1, ack(m, n - 1))
            } else if n == 0 {
                ack(m - 1, 1)
            } else {
                "OutOfRange"
            }
        }
        else if m == 0 && n > 0 {
            n + 1
        } else {
            "OutOfRange"
        }
    }
    var x = ack(3, 4)
    assert(125, x)
}

func foldTest() {
    func fold(xs, fn) {
        var accum = 0
        for x in xs {
            accum = fn(accum, x)
        }
        accum
    }
    var x = fold([1,2,3,4,5,6], (acc,x) => acc + x)
    assert(21, x)
}

func factorsTest() {
    func factors(num) {
        const arr = []
        var x = 1
        while x <= num {
            if num % x == 0 {
                arr.add(x)
            }
            x += 1
        }
        arr
    }
    var x = factors(27)
    assert([1, 3, 9, 27], x)
}

func antiprimeTest() {
    func countDivisors(n) {
        if n < 2 {
            return 1
        }
        var count = 2
        var i = 2
        while i <= n / 2 {
            if n % i == 0 {
                count += 1
            }
            i += 1
        }
        count
    }
    var x = countDivisors(20)
    assert(6, x)
}

func variadicTest1() {
    func takeItAll(x, y, args...) {
        var str = "x=" + y + ";y=" + y + ";other=["
        var count = 0
        for x in args {
            if count > 1 {
                str += ","
            }
            str += x
            count += 1
        }
        str += "]"
    }
    var x = takeItAll(12, 33.4, "oops", true, [1], (x: 0.33, y: 1))
    assert("x=33.4;y=33.4;other=[oopstrue,[1],(x: 0.33, y: 1)]", x)
}

func variadicTest2() {
    func take(args...) {
        args.toString()
    }
    var x = take(1,2,44.42)
    assert("(1, 2, 44.42)", x)
}

func variadicTest3() {
    func take(x, args...) {
        var str = "x=" + x
        str += args
    }
    var x = take(14)
    assert("x=14()", x)
}

func complexToStringTest1() {
    func Integer.toString() {
        if this % 2 == 0 {
            "even"
        } else {
            "odd"
        }
    }
    assert("even", 12.toString())
}

func complexToStringTest2() {
    func Nil.toString() {
        "null"
    }
    var x = "foo" + nil
    assert("foonull", x)
}

func methodTest1() {
    func Integer +(other) {
        this * other
    }
    var res = 3 + 3 == 3 * 3
    Integer.__deleteMember("+")
    assert(true, res)
}

func methodTest2() {
    func Float +(other) {
        this + " + " + other
    }
    var res = 12.2 + 0.4
    Float.__deleteMember("+")
    assert("12.2 + 0.4", res)
}

func functionTest1() {
    func sum(x,y) {
        if y == 2 {
            x + y
        } else {
            var z = sum(y, 2)
            x -= 1
            x + z
        }
    }

    var res = sum(2, 4)
    assert(7, res)
}