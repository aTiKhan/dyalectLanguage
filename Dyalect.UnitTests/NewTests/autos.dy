test init
{
    type Box(key)
    let boxCollector = []
    func Box.dispose() => boxCollector.add(this::key)
}

test "Simple 1"
{
    {
        auto x = Box("foo")
        boxCollector.add("wait.foo")
    }
    assert(["wait.foo", "foo"], boxCollector)
}

test "Simple 2"
{
    {
        auto x = Box("foo")
        boxCollector.add("wait.foo")
        {
            auto y = Box("bar")
            boxCollector.add("wait.bar")
        }
    }
    assert(["wait.foo", "wait.bar", "bar", "foo"], boxCollector)
}

test "Simple 3"
{
    for x in 1..6 {
        auto obj = Box(x)
        if x == 3 {
            continue
        }
        if x == 6 {
            break
        }
    }
    assert([1,2,3,4,5,6], boxCollector)
}

test "Simple 4"
{
    func doit(b) {
        if b {
            auto a = Box(2)
        } else {
            auto b = Box(4)
        }
        auto c = Box(100)
    }
    doit(true)
    doit(false)
    assert([2, 100, 4, 100], boxCollector)
}

test "Simple 5"
{
    var x = 5
    while x > 0 {
        auto a = Box(x)
        if x == 1 {
            break
        }
        x = x - 1
        if x == 3 {
            continue
        }
    }
    assert([5,4,3,2,1], boxCollector)
}

test "Autos in functions 1"
{
    func run() {
        auto x = Box("foo")
        auto y = Box("bar")
    }
    run()
    assert(["foo", "bar"], boxCollector)
}

test "Autos in functions 2"
{
    func iter() {
        auto x = Box("fin")
        boxCollector.add(1)
        yield 1
        boxCollector.add(2)
        yield 2
        boxCollector.add(3)
        yield 3
    }
    for _ in iter() { }
    assert([1,2,3,"fin"], boxCollector)
}

test "Autos in functions 3"
{
    func testFn() {
        auto x = Box("x")
        {
            auto y = Box("y")
            if true {
                auto z = Box("z")
                return
            }
        }
    }
    testFn()
    assert(["z","y","x"], boxCollector)
}

test "Autos in functions 4"
{
    func foobar(flag) {
        auto a = Box('a')
        if flag {
            return
        }
        auto b = Box('b')
    }

    foobar(flag: true)
    foobar(flag: false)
    foobar(flag: true)
    assert(['a', 'a', 'b', 'a'], boxCollector)
}

test "Autos in functions 5"
{
    func iter() {
        auto a = Box(10)
        yield { auto a = Box(2) }
        yield { auto a = Box(4) }
        yield { auto a = Box(6) }
        yield { auto a = Box(8) }
        { auto a = Box(9) }
    }
    for _ in iter() { }
    assert([2,4,6,8,9,10], boxCollector)
}

test "Autos and exceptions 1"
{
    func testE() {
        auto x = Box(12)
        throw "err"
    }

    try {
        testE()
    } catch _ {
        assert([12], boxCollector)
    }
}

test "Autos and exceptions 2"
{
    func test1() {
        auto x = Box("y")
        {
            auto y = Box("z")
            throw "Error"
        }
    }
    func run() {
        auto old = Box("x")
        test1()
    }
    let msg = try {
        run()
    } catch e {
        e[0]
    }
    assert("Error", msg)
    assert(["z", "y", "x"], boxCollector)
}

test "Autos and exceptions 3"
{
    try {
        auto x = Box(42)
        throw 84
    } catch e {
        boxCollector.add(e[0])
    }
    assert([42,84], boxCollector)
}

test "Autos and exceptions 4"
{
    func divide(x, y) {
        auto def = Box(-100)
        try {
            auto box = Box(12)
            return x / y
        } catch _ {
            return def::key
        }
    }
    var xs = [divide(10, 2), divide(2, 0)]
    assert([5, -100], xs)
    assert([12, -100, 12, -100], boxCollector)
}

test "Autos and exceptions 5"
{
    func doit() {
        var x = {
            throw 24
            auto b = Box(12)
        }
    }
    try {
        doit()
    } catch e {
        auto b = Box(e[0])
    }
    assert([24], boxCollector)
}

test "Autos and exceptions 6"
{
    func doit() {
        throw {
            auto b = Box(22)
        }
        auto c = Box(2)
    }
    try {
        doit()
    } catch _ { }
    assert([22], boxCollector)
}

test "Autos in catch 1"
{
    try {
        throw "err"
    } catch e {
        auto b = Box(1)
    }
    assert([1], boxCollector)
}

test "Autos in catch 2"
{
    func inner() {
        try {
            throw "err"
        } catch e {
            auto b = Box(1)
            throw
        }
    }
    try {
        inner()
    } catch e {
        assert(e[0], "err")
        assert([1], boxCollector)
    }
}

test "Autos in catch 3"
{
    func inside() {
        try {
            throw 'a'
        } catch _ {
            auto b = Box("auto")
            throw 'b'
            throw
        }
    }
    var x
    try {
        inside()
    } catch e {
        x = e[0]
    }
    assert('b', x)
}
