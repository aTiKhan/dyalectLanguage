COMPILER Dyalect

CHARACTERS
    cr = '\r'.
    lf = '\n'.
    newLine = cr + lf.
    tab = '\t'.
    letter = 'A' .. 'Z' + 'a' .. 'z' + '_'.
    digit = "0123456789".
    notDigit = ANY - digit.
    hexDigit = digit + "ABCDEFabcdef".
    stringChar = ANY - '"' - '\\' - newLine.
    verbatimChar = ANY - ']'.
    verbatimChar2 = ANY - '>'.
    char = ANY - "'" - newLine.
    esc = digit + letter + "\"\\'~`:;,./][{}!@#$%^&*-_=+?><|".

TOKENS
    identToken  = letter { letter | digit } | "_" letter { letter | digit  }.

    directive = "#" (letter { letter | digit } | "_" letter { letter | digit  }).

    intToken    = digit { digit } | ("0x"|"0X") hexDigit { hexDigit }.

    floatToken  = "." digit { digit } [ ("e"|"E") [ "+" | "-" ] digit { digit } ] [("F"|"f")]
                    | digit { digit } ("F"|"f")
                    | digit { digit }
                        (
                            "." digit { digit } [ ( "e" | "E" ) ["+" | "-" ] digit { digit } ]
                            | ( "e" | "E") ["+" | "-" ] digit { digit }
                        ) [("F"|"f")].

    stringToken = "\"" { stringChar | "\\" esc | ("\\(" {stringChar|"\""} ")" ) }  "\"".
    charToken = "'" { char | "\\" esc } "'".
    implicitToken = "$" digit { digit }.
    verbatimStringToken = "<[" { verbatimChar | ("]" verbatimChar2) | "]>]>" } "]>".

    autoToken           = "auto".
    varToken            = "var".
    letToken            = "let".
    funcToken           = "func".
    returnToken         = "return".
    privateToken        = "private".
    continueToken       = "continue".
    breakToken          = "break".
    yieldToken          = "yield".
    ifToken             = "if".
    forToken            = "for".
    whileToken          = "while".
    typeToken           = "type".
    inToken             = "in".

    arrowToken          = "=>".
    dotToken            = ".".
    commaToken          = ",".
    semicolonToken      = ";".
    colonToken          = ":".
    equalToken          = "=".
    parenLeftToken      = "(".
    parenRightToken     = ")".
    curlyLeftToken      = "{".
    curlyRightToken     = "}".
    squareLeftToken     = "[".
    squareRightToken    = "]".
    eq_coa              = "??=".
    eq_add              = "+=".
    eq_sub              = "-=".
    eq_mul              = "*=".
    eq_div              = "/=".
    eq_rem              = "%=".
    eq_and              = "&&&=".
    eq_or               = "|||=".
    eq_xor              = "^^^=".
    eq_lsh              = "<<<=".
    eq_rsh              = ">>>=".
    minus               = "-".
    plus                = "+".
    not                 = "!".
    bitnot              = "~".
    coalesce            = "??".

COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "//" TO lf

IGNORE cr + lf + tab

PRODUCTIONS

    Separator = ";".

    StandardOperators = "+" | "-" | "*" | "/" | "%" | "|||" | "&&&" | "!"
        | "==" | "!=" | ">" | "<" | ">=" | "<=" | "^^^" | "<<<" | ">>>" | "~~~".

    FunctionName = identToken | StandardOperators | "set".

    Qualident<out string s1, out string s2, out string s3> =
        (. s1 = null; s2 = null; s3 = null; .)
        FunctionName (. s1 = t.val; .)
        [
            ["."] FunctionName  (. s2 = t.val; .)
            [ ["."] FunctionName (. s3 = t.val; .) ]
        ].

    ImportToken<out string str> =
        (. str = ""; .)
        (
            stringToken (. str = ParseSimpleString(); .)
            |
            (
                identToken (. str = t.val; .)
                {
                    "." identToken
                    (. str += string.Concat(".", t.val); .)
                }
            )
        ).

    Import =
        "import"
        (. var inc = new DImport(t); Imports.Add(inc); .)
        ImportToken<out var str> (. var lastName = str; .)
        [
            "="
            ImportToken<out str>
            (.
                inc.Alias = lastName;
                lastName = str;
            .)
        ]
        {
            "/"
            ImportToken<out str>
            (.
                if (inc.LocalPath != null)
                    inc.LocalPath = string.Concat(inc.LocalPath, "/", lastName);
                else
                    inc.LocalPath = lastName;
                lastName = str;
            .)
        }
        (. inc.ModuleName = lastName; .).

    Type<out DNode node> =
        (. DFunctionDeclaration f = null; .)
        "type"
        (.
            var typ = new DTypeDeclaration(t);
            node = typ;
        .)
        identToken (. typ.Name = t.val; node = typ; .)
        [
            (
                (. f = new DFunctionDeclaration(t) { Name = typ.Name, IsStatic = true, IsConstructor = true, TypeName = new Qualident(typ.Name) }; .)
                TypeArguments<f>
                (. typ.Constructors.Add(f); .)
            )
            |
            (
                "="
                identToken (. f = new DFunctionDeclaration(t) { Name = t.val, IsStatic = true, IsConstructor = true, TypeName = new Qualident(typ.Name) }; typ.Constructors.Add(f); .) FunctionArguments<f>
                { "or" identToken (. f = new DFunctionDeclaration(t) { Name = t.val, IsStatic = true, IsConstructor = true, TypeName = new Qualident(typ.Name) }; typ.Constructors.Add(f);.) FunctionArguments<f> }
            )
        ]
        [
            "using" Block<out var cnode>
            (. typ.Using = cnode; .)
        ].
        
    TypeArguments<DFunctionDeclaration node> =
        "("
            [
                TypeArgument<out var arg> (. node.Parameters.Add(arg); .)
                { "," TypeArgument<out arg> (. node.Parameters.Add(arg); .) }
            ]
        ")".
        
    TypeArgument<out DParameter arg> =
        (. arg = null; var mut = false; .)
        [ "var" (. mut = true; .) | "let" ]
        identToken
        (. var ot = t; string str = t.val; .)
        [
            (
                identToken
                (. arg = new DTypeParameter(ot) { Name = t.val, TypeAnnotation = new Qualident(str), Mutable = mut }; .)
            )
            |
            (
                "::" identToken (. var ta = new Qualident(t.val, str); .)
                identToken
                (. arg = new DTypeParameter(ot) { Name = t.val, TypeAnnotation = ta, Mutable = mut }; .)
            )
        ]
        (.
            if (arg is null)
                arg = new DTypeParameter(ot) { Name = str, Mutable = mut };
        .)
        [
            "=" Expr<out var cnode>
            (. arg.DefaultValue = cnode; .)
        ].

    Statement<out DNode node> =
        (. node = null; .)
        (
              Binding<out node> [ Guard<node, out node> ] Separator
            | Rebinding<out node> [ Guard<node, out node> ] Separator
            | ControlFlow<out node> [ Guard<node, out node> ] Separator
            | Type<out node> Separator
            | Match<out node> [ Guard<node, out node> Separator ]
            | IF (IsPrivateScope()) PrivateScope<out node>
            | StatementExpr<out node> [ Guard<node, out node> ] Separator
            | If<out node> [ Guard<node, out node> Separator ]
            | Loops<out node> [ Guard<node, out node> Separator ]
            | Function<out node>
            | Directive<out node> Separator
        )
        (. node = ProcessImplicits(node); .).

    Guard<DNode src, out DNode node> =
        (. node = src; .)
        (
            (. var ot = t; .)
            "when" SimpleExpr<out var cnode>
            (. var @if = new DIf(ot) { Condition = cnode, True = src }; node = @if; .)
            [ "else" SimpleExpr<out cnode> (. @if.False = cnode; .) ]
        ).

    PrivateScope<out DNode node> =
        (. node = null; .)
        (
            "private" (. var ot = t; .)
            Block<out node>
            (.
                node = new DPrivateScope(ot) { Block = (DBlock)node };
            .)
        ).

    StatementExpr<out DNode node> =
        SimpleExpr<out node>
        [
            (.
                var ass = new DAssignment(t) { Target = node };
                node = ass;
                BinaryOperator? op = null;
            .)
            (
                  "="
                | "??=" (. op = BinaryOperator.Coalesce; .)
                | "+="  (. op = BinaryOperator.Add; .)
                | "-="  (. op = BinaryOperator.Sub; .)
                | "*="  (. op = BinaryOperator.Mul; .)
                | "/="  (. op = BinaryOperator.Div; .)
                | "%="  (. op = BinaryOperator.Rem; .)
                | "&&&="  (. op = BinaryOperator.And; .)
                | "|||="  (. op = BinaryOperator.Or; .)
                | "^^^="  (. op = BinaryOperator.Xor; .)
                | "<<<=" (. op = BinaryOperator.ShiftLeft; .)
                | ">>>=" (. op = BinaryOperator.ShiftRight; .)
            )
            Expr<out node>
            (.
                ass.Value = node;
                ass.AutoAssign = op;
                node = ass;
            .)
        ].

    Directive<out DNode node> =
        (
            directive
            (. var pp = new DDirective(t) { Key = t.val.Substring(1) }; node = pp; .)
            {
                (. if (la.AfterEol) return; .)
                (
                      stringToken (. pp.Attributes.Add(ParseSimpleString()); .)
                    | intToken (. pp.Attributes.Add(ParseInteger()); .)
                    | floatToken (. pp.Attributes.Add(ParseFloat()); .)
                    | charToken (. pp.Attributes.Add(ParseChar()); .)
                    | "true" (. pp.Attributes.Add(true); .)
                    | "false" (. pp.Attributes.Add(false); .)
                    | identToken (. pp.Attributes.Add(t.val); .)
                )
            }
        ).

    Block<out DNode node> =
        (. node = null; .)
        (
            "{"
                (. var block = new DBlock(t); .)
                [
                    Statement<out node> (. block.Nodes.Add(node); .)
                    { Statement<out node> (. block.Nodes.Add(node); .) }
                ]
                (. node = block; .)
            "}"
        ).

    Function<out DNode node> =
        (. node = null; var st = false; .)
        (
            [ "static" (. st = true; .) ] "func"
            (.
                var f = new DFunctionDeclaration(t) { IsStatic = st };
                functions.Push(f);
            .)
            Qualident<out var s1, out var s2, out var s3>
            (.
                if (s2 == null && s3 == null)
                    f.Name = s1;
                else if (s3 == null)
                {
                    f.Name = s2;
                    f.TypeName = new Qualident(s1);
                }
                else
                {
                    f.Name = s3;
                    f.TypeName = new Qualident(s2, s1);
                }
            .)
            FunctionArguments<f>
            ( Block<out node> | "=>" Statement<out node> )
            (.
                f.Body = node;
                node = f;
                functions.Pop();
            .)
        ).

    FunctionArgument<out DParameter arg> =
        (. arg = null; .)
        identToken
        (. var ot = t; string str = t.val; .)
        [
            (
                identToken
                (. arg = new DParameter(ot) { Name = t.val, TypeAnnotation = new Qualident(str) }; .)
            )
            |
            (
                "::" identToken (. var ta = new Qualident(t.val, str); .)
                identToken
                (. arg = new DParameter(ot) { Name = t.val, TypeAnnotation = ta }; .)
            )
        ]
        (.
            if (arg is null)
                arg = new DParameter(ot) { Name = str };
        .)
        [
            "=" Expr<out var cnode>
            (. arg.DefaultValue = cnode; .)
        ]
        [ "..." (. arg.IsVarArgs = true; .) ].

    FunctionArguments<DFunctionDeclaration node> =
        "("
            [
                FunctionArgument<out var arg> (. node.Parameters.Add(arg); .)
                { "," FunctionArgument<out arg> (. node.Parameters.Add(arg); .) }
            ]
        ")".

    Binding<out DNode node> =
        (. node = null; .)
        (
            (
                ( "var" | "let" | "const" (. Deprecated("const"); .) )
                (. var bin = new DBinding(t) { Constant = t.val == "let" }; .)
                OrPattern<out var pat> (. bin.Pattern = pat; .)
                [
                    "=" Expr<out node>
                    (. bin.Init = node; .)
                ]
                (. node = bin; .)
            )
            |
            (
                "auto"
                (. var bin = new DBinding(t) { AutoClose = true, Constant = true }; .)
                NamePattern<out var pat> (. bin.Pattern = pat; .)
                "=" Expr<out node>
                (. bin.Init = node; node = bin; .)
            )
        ).

    Rebinding<out DNode node> =
        (
            "set"
            (. var bin = new DRebinding(t); .)
            OrPattern<out var pat> (. bin.Pattern = pat; .)
            "=" Expr<out node>
            (. bin.Init = node; .)
            (. node = bin; .)
        ).

    Expr<out DNode node> =
        (. node = null; .)
        (
              If<out node>
            | SimpleExpr<out node>
            | Loops<out node>
            | Match<out node>
        )
        (. node = ProcessImplicits(node); .).

    //Patterns
    Match<out DNode node> =
        (. node = null; .)
        (
            "match"
            (. var m = new DMatch(t); .)
            Expr<out node>
            (. m.Expression = node; .)
            "{"
                MatchEntry<out var entry> (. m.Entries.Add(entry); .)
                { "," MatchEntry<out entry>  (. m.Entries.Add(entry); .) }
            "}"
            (. node = m; .)
        ).

    MatchEntry<out DMatchEntry me> =
        (
            (. me = new DMatchEntry(t); .)
            OrPattern<out var p>
            (. me.Pattern = p; .)
            [ "when" Expr<out var node> (. me.Guard = node; .) ]
            "=>" StatementExpr<out var exp> (. me.Expression = exp; .)
        ).

    OrPattern<out DPattern node> =
        (. node = null; .)
        AndPattern<out node>
        {
            (. var por = new DOrPattern(t) { Left = node }; .)
            "or" AndPattern<out node>
            (. por.Right = node; node = por; .)
        }.

    AndPattern<out DPattern node> =
        (. node = null; .)
        RangePattern<out node>
        {
            (. var pa = new DAndPattern(t) { Left = node }; .)
            "and" RangePattern<out node>
            (. pa.Right = node; node = pa; .)
        }.

    RangePattern<out DPattern node> =
        (. node = null; .)
        Pattern<out node>
        [
            (. var r = new DRangePattern(t) { From = node }; .)
            ".." Pattern<out node>
            (. r.To = node; node = r; .)
        ].

    Pattern<out DPattern node> =
        (. node = null; .)
        (
              NamePattern<out node> [ AsPattern<node, out node> ]
            | IntegerPattern<out node> [ AsPattern<node, out node> ]
            | FloatPattern<out node> [ AsPattern<node, out node> ]
            | CharPattern<out node> [ AsPattern<node, out node> ]
            | StringPattern<out node> [ AsPattern<node, out node> ]
            | BooleanPattern<out node> [ AsPattern<node, out node> ]
            | NilPattern<out node> [ AsPattern<node, out node> ]
            | IF (IsTuple(allowFields: true)) TuplePattern<out node> [ AsPattern<node, out node> ]
            | GroupPattern<out node> [ AsPattern<node, out node> ]
            | ArrayPattern<out node> [ AsPattern<node, out node> ]
            | MethodCheckPattern<out node> [ AsPattern<node, out node> ]
            | NotPattern<out node>
            | ComparisonPattern<out node>
        ).

    ComparisonPattern<out DPattern node> =
        (. node = null; BinaryOperator op = default; Token ot = default; .)
        (
            (
                  ">" (. op = BinaryOperator.Gt; ot = t; .)
                | "<" (. op = BinaryOperator.Lt; ot = t; .)
                | ">=" (. op = BinaryOperator.GtEq; ot = t; .)
                | "<=" (. op = BinaryOperator.LtEq; ot = t; .)
            )
            (
                  IntegerPattern<out node>
                | FloatPattern<out node>
                | CharPattern<out node>
                | StringPattern<out node>
                | BooleanPattern<out node>
                | NilPattern<out node>                            
            )
            (. node = new DComparisonPattern(ot) { Operator = op, Pattern = node }; .)
        ).

    NotPattern<out DPattern node> =
        "not" (. var np = new DNotPattern(t); .)
        Pattern<out node> (. np.Pattern = node; node = np; .).

    AsPattern<DPattern target, out DPattern node> =
        (. node = null; .)
        (
            (.
                if (la.AfterEol) { node = target; return; }
                var asp = new DAsPattern(t) { Pattern = target };
            .)
            identToken (. asp.Name = t.val; node = asp; .)
        ).

    MethodCheckPattern<out DPattern node> =
        "." identToken (. node = new DMethodCheckPattern(t) { Name = t.val }; .).

    GroupPattern<out DPattern node> = "(" OrPattern<out node> ")".

    ArrayPattern<out DPattern node> =
        (. node = null; .)
        (
            "["
                (. var tup = new DArrayPattern(t); .)
                RangePattern<out node> (. tup.Elements.Add(node); .)
                { "," RangePattern<out node> (. tup.Elements.Add(node); .) }
                (. node = tup; .)
            "]"
        ).

    TuplePattern<out DPattern node> =
        (. node = null; .)
        (
            "("
                (. var tup = new DTuplePattern(t); .)
                (
                      IF (IsLabelPattern()) LabelPattern<out node>
                    | OrPattern<out node>
                )
                (. tup.Elements.Add(node); .)
                {
                    ","
                    [ 
                        (
                              IF (IsLabelPattern()) LabelPattern<out node>
                            | OrPattern<out node>
                        )
                        (. tup.Elements.Add(node); .)
                    ]
                }
                (. node = tup; .)
            ")"
        ).

    LabelPattern<out DPattern node> =
        (. node = null; .)
        (
            identToken
            (. var la = new DLabelPattern(t) { Label = t.val }; .)
            ":" Pattern<out var pat>
            (. la.Pattern = pat; node = la; .)
        ).

    NamePattern<out DPattern node> =
        (. node = null; Token ot = null; string mod = null; string typ = null; string nam = null;.)
        (
            identToken (. nam = t.val; ot = t; .)
            [ "::" identToken (. mod = nam; typ = t.val; nam = null; .) ]
            [ "." identToken (. if (typ is null) typ = nam; nam = t.val; .) ]
            [
                (.
                    var ctor = new DCtorPattern(t);
                    if (mod is null && typ is null)
                        ctor.Constructor = nam;
                    else if (mod is null) {
                        ctor.TypeName = new Qualident(typ);
                        ctor.Constructor = nam;
                    }
                    else {
                        ctor.TypeName = new Qualident(typ, mod);
                        ctor.Constructor = nam;
                    }
                .)
                "("
                    [ 
                        (
                              IF (IsLabelPattern()) LabelPattern<out node>
                            | OrPattern<out node>
                        )
                        (. ctor.Arguments.Add(node); .)
                    ]
                    {
                        ","
                        (
                              IF (IsLabelPattern()) LabelPattern<out node>
                            | OrPattern<out node>
                        )
                        (. ctor.Arguments.Add(node); .)
                    }
                ")"
                (. node = ctor; .)
            ]
            (.
                if (node is null)
                {
                    if (mod is not null && typ is not null && nam is not null)
                        AddError(ParserError.InvalidPattern, t.GetLocation());
                    else if (mod is not null && typ is not null && nam is null)
                    {
                        node = new DTypeTestPattern(ot) {
                            TypeName = typ is null ? new Qualident(nam) : new Qualident(typ, mod) 
                        };
                    }
                    else if (nam is not null)
                    {
                        if (nam == "_")
                            node = new DWildcardPattern(ot);
                        else if (char.IsUpper(nam[0]))
                            node = new DTypeTestPattern(ot) { TypeName = new Qualident(nam) };
                        else
                            node = new DNamePattern(ot) { Name = nam };
                    }
                }
            .)
        ).

    NilPattern<out DPattern node> = "nil" (. node = new DNilPattern(t); .).
    IntegerPattern<out DPattern node> = intToken (. node = new DIntegerPattern(t) { Value = ParseInteger() }; .).
    FloatPattern<out DPattern node> = floatToken (. node = new DFloatPattern(t) { Value = ParseFloat() }; .).
    CharPattern<out DPattern node> = charToken (. node = new DCharPattern(t) { Value = ParseChar() }; .).
    StringPattern<out DPattern node> = stringToken (. node = new DStringPattern(t) { Value = ParseString() }; .).
    BooleanPattern<out DPattern node> = ("true"|"false") (. node = new DBooleanPattern(t) { Value = t.val == "true" }; .).
    //End patterns

    If<out DNode node> =
        (. node = null; .)
        (
            "if"
            (. var @if = new DIf(t); .)
            Expr<out node> (. @if.Condition = node; .)
            Block<out node>  (. @if.True = node; .)
            [
                "else"
                (
                    (
                        Block<out node>
                        (. @if.False = node; .)
                    )
                    |
                    (
                        If<out node>
                        (. @if.False = node; .)
                    )
                )
            ]
            (. node = @if; .)
        ).

    Loops<out DNode node> =
        (. node = null; .)
        (
              While<out node>
            | For<out node>
            | DoWhile<out node> Separator
        ).

    DoWhile<out DNode node> =
        (. node = null; .)
        (
            (. var @while = new DWhile(t) { DoWhile = true }; .)
            "do"
            Block<out node> (. @while.Body = node; .)
            "while" Expr<out node>
            (. @while.Condition = node; node = @while; .)
        ).

    While<out DNode node> =
        (. node = null; .)
        (
            "while"
            (. var @while = new DWhile(t); .)
            Expr<out node> (. @while.Condition = node; .)
            Block<out node>
            (.
                @while.Body = node;
                node = @while;
            .)
        ).

    For<out DNode node> =
        (. node = null; .)
        (
            "for"
            (. var @for = new DFor(t); .)
            OrPattern<out var pattern> (. @for.Pattern = pattern; .)
            "in" Expr<out node> (. @for.Target = node; .)
            [ "when" Expr<out node> (. @for.Guard = node; .) ]
            Block<out node>
            (.
                @for.Body = node;
                node = @for;
            .)
        ).

    ControlFlow<out DNode node> =
        (. node = null; .)
        (
              Break<out node>
            | Continue<out node>
            | Return<out node>
            | Yield<out node>
        ).

    Yield<out DNode node> =
        "yield" 
        (.
            var ot = t;
            node = null;
            functions.Peek().IsIterator = true;
        .)
        (
            ( "break" (. node = new DYieldBreak(ot); .) )
            |
            (
                "many"
                (.
                    var yield = new DYieldMany(t);
                    node = yield;
                .)
                Expr<out var exp> (. yield.Expression = exp; .)
            )
            |
            (
                (.
                    var yield = new DYield(t);
                    node = yield;
                .)
                Expr<out var exp> (. yield.Expression = exp; .)
            )
        ).

    Break<out DNode node> =
        "break"
        (. var br = new DBreak(t); node = br; .)
        [
            (. if (la.AfterEol) return; .)
            Expr<out var exp> (. br.Expression = exp; .)
        ].

    Return<out DNode node> =
        "return"
        (.
            var br = new DReturn(t); node = br;
            if (la.AfterEol) return;
        .)
        [ Expr<out var exp> (. br.Expression = exp; .) ].

    Continue<out DNode node> = "continue" (. node = new DContinue(t); .).

    SimpleExpr<out DNode node> =
        (. node = null; .)
        (
              IF (IsFunction()) FunctionExpr<out node>
            | NoArgFunctionExpr<out node>
            | Coalesce<out node> [ TernaryExpr<node, out node> ]
            | TryCatch<out node>
            | Throw<out node>
            | Block<out node>
        ).

    TernaryExpr<DNode parent, out DNode node> = 
        (. node = null; .)
        (
            "?"
            (. var @if = new DIf(t) { Condition = parent }; .)
            SimpleExpr<out node> (. @if.True = node; .)
            ":" SimpleExpr<out node> (. @if.False = node; node = @if; .)
        ).

    Throw<out DNode node> =
        (. node = null; .)
        (
            "throw" 
            (. 
                var th = new DThrow(t);
                node = th;
                if (la.AfterEol) return;
            .)
            Expr<out var cexp> (. th.Expression = cexp; .)
        ).

    TryCatch<out DNode node> =
        (. node =  null; .)
        (
            "try" (. var tc = new DTryCatch(t); .)
            Block<out node> (. tc.Expression = node; .)
            "catch"
            (
                (
                    (. var m = new DMatch(t); tc.Catch = m; .)
                    "{"
                        MatchEntry<out var entry> (. m.Entries.Add(entry); .)
                        { "," MatchEntry<out entry>  (. m.Entries.Add(entry); .) }
                    "}"
                )
                |
                (
                    identToken (. tc.BindVariable = new DName(t) { Value = t.val }; .)
                    Block<out node> (. tc.Catch = node; .)
                )
            )
            (. node = tc; .)
        ).

    FunctionExpr<out DNode node> =
        (.
            var f = new DFunctionDeclaration(t);
            node = f;
        .)
        (
            (

                  ( FunctionArgument<out var a> (. f.Parameters.Add(a); .) )
                | FunctionArguments<f>
            )
            (. functions.Push(f); .)
            "=>" Expr<out var exp>
            (. f.Body = exp; functions.Pop(); .)
        ).

    NoArgFunctionExpr<out DNode node> =
        (.
            var f = new DFunctionDeclaration(t);
            node = f;
        .)
        (
            (. functions.Push(f); .)
            "=>" Expr<out var exp>
            (. f.Body = exp; functions.Pop(); .)
        ).

    Coalesce<out DNode node> =
        Or<out node>
        {
            "??" (. var ot = t; .) Or<out DNode exp>
            (. node = new DBinaryOperation(node, exp, BinaryOperator.Coalesce, ot); .)
        }.

    Or<out DNode node> =
        And<out node>
        {
            "||" (. var ot = t; .) And<out DNode exp>
            (. node = new DBinaryOperation(node, exp, BinaryOperator.Or, ot); .)
        }.

    And<out DNode node> =
        Is<out node>
        {
            "&&" (. var ot = t; .) Is<out DNode exp>
            (. node = new DBinaryOperation(node, exp, BinaryOperator.And, ot); .)
        }.

    Is<out DNode node> =
        In<out node>
        {
            "is" (. var ot = t; .)
            OrPattern<out var pat>
            (. node = new DBinaryOperation(node, pat, BinaryOperator.Is, ot); .)
        }.

    In<out DNode node> =
        Range<out node>
        {
            "in" (. var ot = t; .)
            Range<out var pat>
            (. node = new DBinaryOperation(node, pat, BinaryOperator.In, ot); .)
        }.

    Range<out DNode node> =
        (. node = null; DNode snode = null; DNode cnode = null; bool exclu = false; .)
        (
            (
                "^" FunctionApplication<out snode>
                ( ".." | "..<" (. exclu = true; .) )
                (. var range = new DRange(t) { From = node, Exclusive = exclu, Step = snode }; node = range; .)
                [ FunctionApplication<out cnode> (. range.To = cnode; .) ]
            )
            |
            (
                ( ".." | "..<" (. exclu = true; .) )
                (. var range = new DRange(t) { From = node, Exclusive = exclu }; node = range; .)
                [ FunctionApplication<out cnode> (. range.To = cnode; .) ]
            )
            |
            (
                Eq<out node> [ "^" FunctionApplication<out snode> ]
                [
                    ( ".." | "..<" (. exclu = true; .) )
                    (. var range = new DRange(t) { From = node, Exclusive = exclu, Step = snode }; node = range; .)
                    [ FunctionApplication<out cnode> (. range.To = cnode; .) ]
                ]
            )
        ).

    Eq<out DNode node> =
        Shift<out node>
        {
            (.
                var op = default(BinaryOperator);
                var ot = default(Token);
            .)
            (
                  ">"  (. ot = t; op = BinaryOperator.Gt; .)
                | "<"  (. ot = t; op = BinaryOperator.Lt; .)
                | ">=" (. ot = t; op = BinaryOperator.GtEq; .)
                | "<=" (. ot = t; op = BinaryOperator.LtEq; .)
                | "==" (. ot = t; op = BinaryOperator.Eq; .)
                | "!=" (. ot = t; op = BinaryOperator.NotEq; .)
            )
            (
                Shift<out var exp> (. node = new DBinaryOperation(node, exp, op, ot); .)
            )
        }.

    Shift<out DNode node> =
        BitOr<out node>
        {
            (.
                var op = default(BinaryOperator);
                var ot = default(Token);
            .)
            (
                  "<<<" (. ot = t; op = BinaryOperator.ShiftLeft; .)
                | ">>>" (. ot = t; op = BinaryOperator.ShiftRight; .)
            )
            (
                BitOr<out var exp> (. node = new DBinaryOperation(node, exp, op, ot); .)
            )
        }.

    BitOr<out DNode node> =
        Xor<out node>
        {
            "|||" (. var ot = t; .)
            (
                Xor<out var exp> (. node = new DBinaryOperation(node, exp, BinaryOperator.BitwiseOr, ot); .)
            )
        }.

    Xor<out DNode node> =
        BitAnd<out node>
        {
            (. DNode exp = null; .)
            "^^^" (. var ot = t; .)
            (
                BitAnd<out exp> (. node = new DBinaryOperation(node, exp, BinaryOperator.Xor, ot); .)
            )
        }.

    BitAnd<out DNode node> =
        Add<out node>
        {
            "&&&" (. var ot = t; .)
            (
                Add<out var exp> (. node = new DBinaryOperation(node, exp, BinaryOperator.BitwiseAnd, ot); .)
            )
        }.

    Add<out DNode node> =
        Mul<out node>
        {
            (.
                var op = default(BinaryOperator);
                var ot = default(Token);
            .)
            (
                (
                    //(. if (la.AfterEol) return; .)
                    "+" (. ot = t; op = BinaryOperator.Add; .)
                )
                |
                (
                    //(. if (la.AfterEol) return; .)
                    "-" (. ot = t; op = BinaryOperator.Sub; .)
                )
            )
            (
                Mul<out var exp> (. node = new DBinaryOperation(node, exp, op, ot); .)
            )
        }.

    Mul<out DNode node> =
        FunctionApplication<out node>
        {
            (.
                var op = default(BinaryOperator);
                var ot = default(Token);
            .)
            (
                  "*" (. ot = t; op = BinaryOperator.Mul; .)
                | "/" (. ot = t; op = BinaryOperator.Div; .)
                | "%" (. ot = t; op = BinaryOperator.Rem; .)
            )
            (
                FunctionApplication<out var exp> (. node = new DBinaryOperation(node, exp, op, ot); .)
            )
        }.

    FunctionApplication<out DNode node> =
        (. node = null; .)
        (
            Unary<out node>
            {
                (. var app = new DApplication(node, t); .)
                "\\" Unary<out node>
                (. app.Arguments.Add(node); node = app; .)
            }
        ).

    Unary<out DNode node> =
        (.
            node = null;
            var op = default(UnaryOperator);
            var ot = default(Token);
        .)
        (
            (
                (
                    "!" (. ot = t; op = UnaryOperator.Not; .)
                    Index<out node>
                    (. node = new DUnaryOperation(node, op, ot); .)
                )
                |
                (
                    "-" (. ot = t; op = UnaryOperator.Neg; .)
                    Index<out node>
                    (. node = new DUnaryOperation(node, op, ot); .)
                )
                |
                (
                    "+" (. ot = t; op = UnaryOperator.Plus; .)
                    Index<out node>
                    (. node = new DUnaryOperation(node, op, ot); .)
                )
                |
                (
                    "~~~" (. ot = t; op = UnaryOperator.BitwiseNot; .)
                    Index<out node>
                    (. node = new DUnaryOperation(node, op, ot); .)
                )
                | Index<out node>
            )
        ).

    Index<out DNode node> =
        (. var nsaf = false; .)
        (
            Literal<out node> 
            {
                (
                    (
                        ( "." | "?." (. nsaf = true; .) )
                        (. var ot = t; .)
                        identToken
                        (.
                            var fld = new DAccess(ot) { Target = node, NilSafety = nsaf };
                            fld.Name = t.val;
                            node = fld;
                        .)
                    )
                    |
                    (
                        ( "::" | "?::" (. nsaf = true; .) )
                        (. var ot = t; .)
                        identToken
                        (.
                            node = new DIndexer(t) { Target = node, NilSafety = nsaf, Index = new DStringLiteral(ot) { Value = t.val } };
                        .)
                    )
                    |
                    (
                        (. if (la.AfterEol) return; .)
                        ( "[" | "?[" (. nsaf = true; .) )
                            (. var idx = new DIndexer(t) { Target = node, NilSafety = nsaf }; .)
                            Expr<out node>
                            (.
                                idx.Index = node;
                                node = idx;
                            .)
                        "]"
                    )
                    |
                    (
                        (. if (la.AfterEol) return; .)
                        ( "(" | "?(" (. nsaf = true; .) )
                            (. var app = new DApplication(node, t) { NilSafety = nsaf }; .)
                            [ ApplicationArguments<app> ]
                            (. node = app; .)
                        ")"
                    )
                )
            }
        ).

    ApplicationArguments<DApplication app> =
        (. var node = default(DNode); .)
        (
              IF (IsLabel()) Label<out node>
            | Expr<out node> 
        )
        (. app.Arguments.Add(node); .)
        {
            ","
            (
                  IF (IsLabel()) Label<out node>
                | Expr<out node> 
            )
            (. app.Arguments.Add(node); .)
        }.

    Literal<out DNode node> =
        (. node = null; .)
        (
              Name<out node>
            | SpecialName<out node>
            | Integer<out node>
            | Float<out node>
            | String<out node>
            | Char<out node>
            | Bool<out node>
            | Nil<out node>
            | IF (IsTuple()) Tuple<out node>
            | Group<out node>
            | Base<out node>
            | Array<out node>
            | Iterator<out node>
        ).

    Iterator<out DNode node> =
        (. node = null; .)
        (
            "yields" "{"
                (.
                    var it = new DIteratorLiteral(t);
                    it.YieldBlock = new DYieldBlock(t);
                .)
                [ Expr<out node> (. it.YieldBlock.Elements.Add(node); .) ]
                { "," Expr<out node> (. it.YieldBlock.Elements.Add(node); .) }
                (. node = it; .)
            "}"
        ).

    Label<out DNode node> =
        (. bool mut = false; node = null; var name = ""; .)
        (
            [ "var" (. mut = true; .) | "let" ]
            ( identToken (. name = t.val; .) | stringToken (. name = ParseSimpleString(); .) )
            ":" (. var ot = t; .)
            (
                  IF (IsFunction()) FunctionExpr<out node>
                | Is<out node>
            )
            (. node = new DLabelLiteral(ot) { Mutable = mut, Label = name, Expression = node }; .)
        ).

    Base<out DNode node> =
        "base" (. node = new DBase(t); .).

    Array<out DNode node> =
        (. node = null; .)
        (
            "["
                (. var arr = new DArrayLiteral(t); .)
                [
                    Expr<out node> (. arr.Elements.Add(node); .)
                    { "," Expr<out node> (. arr.Elements.Add(node); .) }
                ]
                (. node = arr; .)
            "]"
        ).

    Tuple<out DNode node> =
        (. node = null; .)
        (
            "("
                (.  var tup = new DTupleLiteral(t); .)
                (
                      IF (IsLabel()) Label<out node>
                    | Expr<out node>
                )
                (. tup.Elements.Add(node); .)
                { 
                    ","
                    [
                        (
                              IF (IsLabel()) Label<out node>
                            | Expr<out node>
                        )
                        (. tup.Elements.Add(node); .)
                    ] 
                }
                (. node = tup; .)
            ")"
        ).

    Group<out DNode node> =
        (. node = null; .)
        (
            "(" Expr<out node> ")"
        ).

    Name<out DNode node> =
        (
            (identToken | "private")
            (. node = new DName(t) { Value = t.val }; .)
        ).

    SpecialName<out DNode node> =
        (
            implicitToken
            (.
                var nm = int.Parse(t.val.Substring(1));
                node = new DName(t) { Value = "p" + nm };
                if (implicits == null)
                    implicits = new List<int>();
                implicits.Add(nm);
            .)
        ).

    Nil<out DNode node> = "nil" (. node = new DNilLiteral(t); .).

    Char<out DNode node> =
        (
            charToken
            (. node = new DCharLiteral(t) { Value = ParseChar() }; .)
        ).

    Integer<out DNode node> =
        (
            intToken
            (. node = new DIntegerLiteral(t) { Value = ParseInteger() }; .)
        ).

    Float<out DNode node> =
        (
            floatToken
            (. node = new DFloatLiteral(t) { Value = ParseFloat() }; .)
        ).

    String<out DNode node> =
        (. node = null; .)
        (
            stringToken (. node = ParseString(); .)
            | verbatimStringToken (. node = ParseVerbatimString(); .)
        ).

    Bool<out DNode node> =
        (
            ( "true" | "false" )
            (. node = new DBooleanLiteral(t) { Value = t.val == "true" }; .)
        ).

    DyalectItem =
        (
              (
                Statement<out var node>
                (.
                    Root.Nodes.Add(node);
                .)
              )
            | ( Import Separator )
        ).

    Dyalect =
        (
            DyalectItem { DyalectItem }
        ).

END Dyalect.
